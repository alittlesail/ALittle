
namespace ALittle;

private var __cos = Math_Cos;
private var __sin = Math_Sin;

public class ImageInput : DisplayLayout
{
    private TextInput _show_input;
    
    private double _logic_left;
    private double _logic_right;
    private double _logic_top;
    private double _logic_bottom;
    
    private int _ims_padding;
    
    private DisplayObject _show_up;
    private DisplayObject _show_over;
    private DisplayObject _show_down;
    private DisplayObject _show_disabled;

    public ctor(ControlSystem ctrl_sys)
    {
        //创建TextInput控件
        this._show_input = new TextInput(this._ctrl_sys);
        this._show_input.x_type = UIEnumTypes.POS_ALIGN_STARTING;
        this._show_input.y_type = UIEnumTypes.POS_ALIGN_STARTING;
        this._show_input.width_type = UIEnumTypes.SIZE_MARGIN;
        this._show_input.width_value = 0;
        this._show_input.height_type = UIEnumTypes.SIZE_MARGIN;
        this._show_input.height_value = 0;
        this.AddChild(this._show_input, 5);
    
        //存放逻辑数据
        this._logic_left = 0;
        this._logic_right = 0;
        this._logic_top = 0;
        this._logic_bottom = 0;
    
        // 额外的ims范围
        this._ims_padding = 0;
    
        //底图初始化
        // this._show_up = null;
        // this._show_over = null;
        // this._show_down = null;
        this._show_disabled = null;
    
        this._pickup_this = true;
    
        // 侦听事件
        this._show_input.AddEventListener(this, this.HandleFocusIn);
        this._show_input.AddEventListener(this, this.HandleFocusOut);
        this._show_input.AddEventListener(this, this.HandleMoveIn);
        this._show_input.AddEventListener(this, this.HandleMoveOut);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformLButtonDown);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformTextInput);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformKeyDown);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformDragBegin);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformDrag);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformDragEnd);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformMButtonWheel);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformChangd);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformEnterKey);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformTabKey);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformRButtonDown);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformRButtonUp);
        this._show_input.AddEventListener(this, this.HandleImageInputTransformLongButtonDown);
    }
    ////////-属性设置////////////////////////////////////////////////////////////////////////

    public set disabled(bool value)
    {
        DisplayObject.disabled(this, value);
        if (value)
            this.ShowDisabled();
        else
            this.ShowUp();
    }
    
    // 控件焦点
    public get is_focus() : bool
    {
        return A_UISystem.focus == this._show_input;
    }
    // 控件焦点
    public set focus(bool value)
    {
        if (value)
            A_UISystem.focus = this._show_input;
        else
        {
            if (A_UISystem.focus == this._show_input)
                A_UISystem.focus = null;
        }
    }
    
    public get is_input() : bool
    {
        return true;
    }
    
    public get regex() : string
    {
        return this._show_input.regex;
    }
    
    public set regex(string value)
    {
        if (value == null) value = "";
        this._show_input.regex = value;
    }
    
    public get limit_len() : int
    {
        return this._show_input.limit_len;
    }
    
    public set limit_len(int value)
    {
        if (value == null) value = 0;
        this._show_input.limit_len = value;
    }
    
    public set default_text(string value)
    {
        this._show_input.default_text = value;
    }
    
    public get default_text() : string
    {
        return this._show_input.default_text;
    }
    
    public set default_text_alpha(double value)
    {
        this._show_input.default_text_alpha = value;
    }
    
    public get default_text_alpha() : double
    {
        return this._show_input.default_text_alpha;
    }
    
    // 获取光标偏移位置X
    public get cursor_x() : double
    {
        return this._show_input.cursor_x;
    }
    
    // 获取光标偏移位置Y
    public get cursor_y() : double
    {
        return this._show_input.cursor_y;
    }
    
    // 设置是否可编辑
    public set editable(bool value)
    {
        this._show_input.editable = value;
    }
    
    // 获取是否可编辑
    public get editable() : bool
    {
        return this._show_input.editable;
    }
    
    // 设置字体，以及字体大小
    public set font_path(string value)
    {
        this._show_input.font_path = value;
    }
    public set font_size(int value)
    {
        this._show_input.font_size = value;
    }
    
    public get font_path() : string
    {
        return this._show_input.font_path;
    }
    public get font_size() : int
    {
        return this._show_input.font_size;
    }
    
    // 设置文本
    public set text(any value)
    {
        this._show_input.text = value;
    }
    // 获取文本
    public get text() : string
    {
        return this._show_input.text;
    }
    
    // 设置粗体
    public set bold(bool value)
    {
        this._show_input.bold = value;
    }
    public get bold() : bool
    {
        return this._show_input.bold;
    }
    
    // 设置斜体
    public set italic(bool value)
    {
        this._show_input.italic = value;
    }
    public get italic() : bool
    {
        return this._show_input.italic;
    }
    
    // 设置下划线
    public set underline(bool value)
    {
        this._show_input.underline = value;
    }
    public get underline() : bool
    {
        return this._show_input.underline;
    }
    
    // 设置删除线
    public set deleteline(bool value)
    {
        this._show_input.deleteline = value;
    }
    public get deleteline() : bool
    {
        return this._show_input.deleteline;
    }
    
    // 设置密码模式
    public set password_mode(bool value)
    {
        this._show_input.password_mode = value;
    }
    public get password_mode() : bool
    {
        return this._show_input.password_mode;
    }
    
    // 光标颜色
    public set cursor_red(double value)
    {
        this._show_input.cursor_red = value;
    }
    public get cursor_red() : double
    {
        return this._show_input.cursor_red;
    }
    public set cursor_green(double value)
    {
        this._show_input.cursor_green = value;
    }
    public get cursor_green() : double
    {
        return this._show_input.cursor_green;
    }
    public set cursor_blue(double value)
    {
        this._show_input.cursor_blue = value;
    }
    public get cursor_blue() : double
    {
        return this._show_input.cursor_blue;
    }
    
    public set red(double value)
    {
        this._show_input.red = value;
    }
    public get red() : double
    {
        return this._show_input.red;
    }
    public set green(double value)
    {
        this._show_input.green = value;
    }
    public get green() : double
    {
        return this._show_input.green;
    }
    public set blue(double value)
    {
        this._show_input.blue = value;
    }
    public get blue() : double
    {
        return this._show_input.blue;
    }
    
    public set ims_padding(int value)
    {
        this._ims_padding = value;
        this._show_input.ims_padding = Math_Floor(value + this._logic_bottom);
    }
    
    public get ims_padding() : int
    {
        return this._ims_padding;
    }
    
    ////////-事件处理////////////////////////////////////////////////////////////////////////
    
    // 处理焦点事件
    protected fun HandleFocusIn(UIFocusInEvent event)
    {
        event.target = this;
        this.DispatchEvent(event);
        this.ShowDown();
    }
    
    protected fun HandleFocusOut(UIFocusOutEvent event)
    {
        event.target = this;
        this.DispatchEvent(event);
        this.ShowUp();
    }
    
    // 处理发送事件
    protected fun HandleImageInputTransformLButtonDown(UILButtonDownEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformTextInput(UITextInputEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformKeyDown(UIKeyDownEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformDragBegin(UIButtonDragBeginEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformDrag(UIButtonDragEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformDragEnd(UIButtonDragEndEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformMButtonWheel(UIMButtonWheelEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformChangd(UIChangedEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformEnterKey(UIEnterKeyEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformTabKey(UITabKeyEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformRButtonDown(UIRButtonDownEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformRButtonUp(UIRButtonUpEvent event) { event.target = this; this.DispatchEvent(event); }
    protected fun HandleImageInputTransformLongButtonDown(UILongButtonDownEvent event) { event.target = this; this.DispatchEvent(event); }
    
    
    // 鼠标移入
    protected fun HandleMoveIn(UIMoveInEvent event)
    {
        event.target = this;
        this.DispatchEvent(event);
        if (A_UISystem.focus != this._show_input)
            this.ShowOver();
    }
    
    // 鼠标移出
    protected fun HandleMoveOut(UIMoveOutEvent event)
    {
        event.target = this;
        this.DispatchEvent(event);
        if (A_UISystem.focus != this._show_input)
            this.ShowUp();
    }
    
    ////////////////////////////////////////////////////////////////////////////////-
    protected fun ShowUp()
    {
        if (this._abs_disabled || this._disabled)
            return;
            
        if (this._show_up != null)
            this._show_up.alpha = 1;
        if (this._show_over != null)
            this._show_over.alpha = 0;
        if (this._show_down != null)
            this._show_down.alpha = 0;
        if (this._show_disabled != null)
            this._show_disabled.alpha = 0;
    }

    protected fun ShowDown()
    {
        if (this._abs_disabled || this._disabled)
            return;
    
        if (this._show_up != null)
            this._show_up.alpha = 0;
        if (this._show_over != null)
            this._show_over.alpha = 0;
        if (this._show_down != null)
            this._show_down.alpha = 1;
        if (this._show_disabled != null)
            this._show_disabled.alpha = 0;
    }

    protected fun ShowOver()
    {
        if (this._abs_disabled || this._disabled)
            return;
    
        if (this._show_up != null)
            this._show_up.alpha = 0;
        if (this._show_over != null)
            this._show_over.alpha = 1;
        if (this._show_down != null)
            this._show_down.alpha = 0;
        if (this._show_disabled != null)
            this._show_disabled.alpha = 0;
    }

    protected fun ShowDisabled()
    {
        if (this._show_up != null)
            this._show_up.alpha = 0;
        if (this._show_over != null)
            this._show_over.alpha = 0;
        if (this._show_down != null)
            this._show_down.alpha = 0;
        if (this._show_disabled != null)
            this._show_disabled.alpha = 1;
    }
    
    ////////////////////////////////////////////////////////////////////////////////-
    
    public set show_up(DisplayObject value)
    {
        this.RemoveChild(this._show_up);
        this._show_up = value;
        if (this._show_up != null)
        {
            this._show_up.width_type = UIEnumTypes.SIZE_MARGIN;
            this._show_up.height_type = UIEnumTypes.SIZE_MARGIN;
            this._show_up.width_value = 0;
            this._show_up.height_value = 0;
            this._show_up.disabled = true;
            this.AddChild(this._show_up, 1);
        }
        this.ShowUp();
    }
    
    public get show_up() : DisplayObject
    {
        return this._show_up;
    }
    
    public set show_down(DisplayObject value)
    {
        this.RemoveChild(this._show_down);
        this._show_down = value;
        if (this._show_down != null)
        {
            this._show_down.width_type = UIEnumTypes.SIZE_MARGIN;
            this._show_down.height_type = UIEnumTypes.SIZE_MARGIN;
            this._show_down.width_value = 0;
            this._show_down.height_value = 0;
            this._show_down.disabled = true;
            this.AddChild(this._show_down, 1);
        }
        this.ShowUp();
    }
    
    public get show_down() : DisplayObject
    {
        return this._show_down;
    }
    
    public set show_over(DisplayObject value)
    {
        this.RemoveChild(this._show_over);
        this._show_over = value;
        if (this._show_over != null)
        {
            this._show_over.width_type = UIEnumTypes.SIZE_MARGIN;
            this._show_over.height_type = UIEnumTypes.SIZE_MARGIN;
            this._show_over.width_value = 0;
            this._show_over.height_value = 0;
            this._show_over.disabled = true;
            this.AddChild(this._show_over, 1);
        }
        this.ShowUp();
    }
    
    public get show_over() : DisplayObject
    {
        return this._show_over;
    }
    
    public set show_disabled(DisplayObject value)
    {
        this.RemoveChild(this._show_disabled);
        this._show_disabled = value;
        if (this._show_disabled != null)
        {
            this._show_disabled.width_type = UIEnumTypes.SIZE_MARGIN;
            this._show_disabled.height_type = UIEnumTypes.SIZE_MARGIN;
            this._show_disabled.width_value = 0;
            this._show_disabled.height_value = 0;
            this._show_disabled.disabled = true;
            this.AddChild(this._show_disabled, 1);
        }
    
        this.ShowUp();
    }
    
    public get show_disabled() : DisplayObject
    {
        return this._show_disabled;
    }
    
    public get show_input() : TextInput
    {
        return this._show_input;
    }
    
    ////////////////////////////////////////////////////////////////////////////////-
    
    public set margin_left(double value)
    {
        this._logic_left = value;
        this.Layout();
    }
    public get margin_left() : double
    {
        return this._logic_left;
    }
    
    public set margin_right(double value)
    {
        this._logic_right = value;
        this.Layout();
    }
    public get margin_right() : double
    {
        return this._logic_right;
    }
    
    public set margin_top(double value)
    {
        this._logic_top = value;
        this.Layout();
    }
    public get margin_top() : double
    {
        return this._logic_top;
    }
    
    public set margin_bottom(double value)
    {
        this._logic_bottom = value;
        this.Layout();
        this._show_input.ims_padding = Math_Floor(this._ims_padding + this._logic_bottom);
    }
    public get margin_bottom() : double
    {
        return this._logic_bottom;
    }
    // 更新输入框布局
    public fun Layout()
    {
        this.SetChildIndex(this._show_input, 5);
        this._show_input.x_value = this._logic_left;
        this._show_input.y_value = this._logic_top;
        this._show_input.width_value = this._logic_left + this._logic_right;
        this._show_input.height_value = this._logic_top + this._logic_bottom;
    }
    
    // 检查坐标(x, y)是否在控件范围内，返回被点中的控件
    public fun PickUp(double x, double y) : DisplayObject, double, double
    {
        // 检查是否被禁用，是否可见
        if (this._ignore || this._abs_disabled || this._abs_visible == false)
            return null, null, null;
    
        // 计算出相对于控件的点
        var rel_x = x - this._x;
        var rel_y = y - this._y;
    
        // 移到中心点
        var xx = rel_x - this._center_x;
        var yy = rel_y - this._center_y;
    
        // 处理旋转
        if (this._angle != 0)
        {
            // 逆旋转
            var rad = 3.1415926 * -this._angle / 180.0;
            var cos = __cos(rad);
            var sin = __sin(rad);
            var xxx = xx * cos + yy * -sin;
            var yyy = xx * sin + yy * cos;
    
            xx = xxx;
            yy = yyy;
        }
    
        // 处理缩放
        if (this._scale_x > 0) xx /= this._scale_x;
        if (this._scale_y > 0) yy /= this._scale_y;
    
        // 移动到原来的位置
        rel_x = xx + this._center_x;
        rel_y = yy + this._center_y;
    
        if (this._scale_x <= 0 || this._scale_y <= 0)
        {
            if (this._modal)
                return this, rel_x, rel_y;
            return null, rel_x, rel_y;
        }
    
        // 检查是否在当前矩形范围
        if (this._pickup_rect && (rel_x < 0 || rel_y < 0 || rel_x >= this._width || rel_y >= this._height))
        {
            if (this._modal)
                return this, rel_x, rel_y;
            return null, null, null;
        }
    
        // 检查是否拾取子控件
        if (this._pickup_child)
        {
            var v_focus, v_x, v_y = this._show_input.PickUp(rel_x, rel_y);
            // 如果有移动焦点，那个可以直接返回
            return this._show_input, v_x, v_y;
        }
    
        if (this._modal || (this._pickup_rect && this._pickup_child == false) || this._pickup_this)
            return this, rel_x, rel_y;

        return null, null, null;
    }
    
    //////////////////////////////////////////////////////////////////////////-
    
    // 设置光标到开始位置
    public fun SetCursorToHome()
    {
        this._show_input.SetCursorToHome();
    }
    
    // 设置光标到结尾位置
    public fun SetCursorToEnd()
    {
        this._show_input.SetCursorToEnd();
    }
    
    // 全选
    public fun SelectAll()
    {
        this._show_input.SelectAll();
    }
    
    // 复制选中
    public fun CopyText()
    {
        this._show_input.CopyText();
    }
    
    // 粘帖
    public fun PasteText()
    {
        this._show_input.PasteText();
    }
    
    // 剪切
    public fun CutText()
    {
        this._show_input.CutText();
    }
    
    // 插入文本
    public fun InsertText(string text)
    {
        this._show_input.InsertText(text);
    }
}