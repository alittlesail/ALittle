namespace FileServer;

struct HeadImageInfo : ALittle.CacheData
{
    string image_md5;
    int create_time;
}

// 待删除的头像列表
struct HeadImageInfoDelete
{
    int account_id;         // 账号ID
    int create_time;        // 创建时间
}

public class HeadImageManager
{
    private ALittle.IMsgCommon _gw_session;         // Gateway连接
    private string _base_path;
    private int _timer;      // 删除定时器
    private ALittle.CacheDataSet<HeadImageInfo> _cache;
    
    public ctor()
    {
        this._cache = new ALittle.CacheDataSet<HeadImageInfo>(30*1000, 60*1000);
    }
    
    public await fun Setup() : string
    {
        var error = this._cache.Init();
        if (error != null) return error;
        
        this._base_path = g_ConfigSystem.GetString("head_image_path", "");
        
        // 创建文件夹
        if (this._base_path != "") ALittle.File_MakeDeepDir(this._base_path);
        
        A_SessionSystem.AddEventListener(this, this.HandleSessionDisconnect);
        A_SessionSystem.AddEventListener(this, this.HandleSessionConnect);
        A_SessionSystem.AddConnectSession(ALittle.RouteType.RT_GATEWAY, ALittle.RouteNum.RN_DEFAULT);
        
        // 每天定时运行清理
        var delay_time = ALittle.GetNextTodayBeginTime() - lua.os.time();
        this._timer = A_LoopSystem.AddTimer(delay_time * 1000, bind(this.HandleNewDay, this));
        
        return null;
    }
    
    public fun Shutdown()
    {
        if (this._timer != null)
        {
            A_LoopSystem.RemoveTimer(this._timer);
            this._timer = null;
        }
        this._cache.Release();
    }
    
    public fun GetImagePath(int id, int create_time) : string
    {
        var ymd = lua.os.date("%Y-%m-%d", create_time);
        var hms = lua.os.date("%H-%M-%S", create_time);
        
	    return this._base_path.."/"..ymd.."/"..hms.."/"..id.."_"..ymd.."_"..hms..".png";
    }
    
    public get cache() : ALittle.CacheDataSet<HeadImageInfo> { return this._cache; }
    
    private async fun HandleNewDay()
    {
        var error, select_list = A_MysqlSystem.SelectListFromByMap{HeadImageInfoDelete}(null);
        if (error != null) ALittle.Error(error);
        
        if (select_list != null)
        {
            for (var index, info in select_list)
            {
                var file_path = this.GetImagePath(info.account_id, info.create_time);            
                if (!ALittle.File_DeleteFile(file_path))
			        ALittle.Warn("文件删除失败:"..file_path);
            }
        }
        error = A_MysqlSystem.DeleteFromByMap{HeadImageInfo}(null);
        if (error != null) ALittle.Error(error);
        
        // 定时明天凌晨
        var delay_time = ALittle.GetNextTodayBeginTime() - lua.os.time();
        this._timer = A_LoopSystem.AddTimer(delay_time * 1000, bind(this.HandleNewDay, this));
    }
    
    // 处理GW断开连接
    protected fun HandleSessionDisconnect(ALittle.SessionDisconnectEvent event)
    {
        if (event.route_type != ALittle.RouteType.RT_GATEWAY) return;
        this._gw_session = null;
    }

    // 处理连接成功
    protected fun HandleSessionConnect(ALittle.SessionConnectEvent event)
    {
        if (event.route_type != ALittle.RouteType.RT_GATEWAY) return;
        
        // 保存连接
        this._gw_session = event.session;
        
        // 上报服务器相关信息
        var param = new GatewayServer.NUpdateRouteInfo();
        param.route_weight = 0;
        param.route_info = new GatewayServer.ConnectInfo();
        param.route_info.http_ip = __CPPAPI_ServerSchedule.GetHttpServerYunIp();
        if (param.route_info.http_ip == null || param.route_info.http_ip == "")
            param.route_info.http_ip = __CPPAPI_ServerSchedule.GetHttpServerIp();
        param.route_info.http_port = __CPPAPI_ServerSchedule.GetHttpServerPort();
        event.session.SendMsg(param);
    }
}

protected var g_HeadImageManager = new HeadImageManager();

public struct QCheckHeadImage
{
    int gs_route_id;
    int account_id;
    string session_code;
    int target_account_id;
    string target_image_md5;
}

public struct ACheckHeadImage
{
    bool is_exist;      // 是否存在
    bool need_download; // 是否需要下载
}

[Http]
public await static HandleCheckHeadImage(ALittle.IHttpReceiver client, QCheckHeadImage msg) : ACheckHeadImage
{
    var error, session = A_SessionSystem.ConnectSession(ALittle.RouteType.RT_GAME, msg.gs_route_id);
    if (error != null) throw(error);
    
    {
        var param = new ALittle.SS2GS_QCheckSessionCode();
        param.account_id = msg.account_id;
        param.session_code = msg.session_code;
        error = ALittle.HandleQCheckSessionCode(session, param);
        if (error != null) throw(error);
    }
    
    {
        var data = g_HeadImageManager.cache.GetData(msg.target_account_id);
        if (data == null)
        {
            var param = new ACheckHeadImage();
            param.is_exist = false;
            return param;
        }
        
        var param = new ACheckHeadImage();
        param.is_exist = true;
        param.need_download = data.image_md5 != msg.target_image_md5;
        return param;
    }
}

public struct QDownloadHeadImage
{
    int gs_route_id;
    int account_id;
    string session_code;
    int target_account_id;
}

[HttpDownload]
public await static HandleDownloadHeadImage(ALittle.IHttpReceiver client, QDownloadHeadImage msg) : string, int
{
    var error, session = A_SessionSystem.ConnectSession(ALittle.RouteType.RT_GAME, msg.gs_route_id);
    if (error != null) throw(error);
    
    {
        var param = new ALittle.SS2GS_QCheckSessionCode();
        param.account_id = msg.account_id;
        param.session_code = msg.session_code;
        error = ALittle.HandleQCheckSessionCode(session, param);
        if (error != null) throw(error);
    }
    
    {
        var data = g_HeadImageManager.cache.GetData(msg.target_account_id);
        assert(data, "头像不存在");
        
        return g_HeadImageManager.GetImagePath(data.id, data.create_time), 0;
    }
}


public struct QUploadHeadImage
{
    int gs_route_id;
    int account_id;
    string session_code;
    string target_image_md5;
}

public struct AUploadHeadImage
{
    
}

[HttpUpload]
public await static HandleQUploadHeadImage(ALittle.IHttpFileReceiver client, QUploadHeadImage msg)
{
    var error, session = A_SessionSystem.ConnectSession(ALittle.RouteType.RT_GAME, msg.gs_route_id);
    if (error != null) throw(error);
    
    {
        var param = new ALittle.SS2GS_QCheckSessionCode();
        param.account_id = msg.account_id;
        param.session_code = msg.session_code;
        error = ALittle.HandleQCheckSessionCode(session, param);
        if (error != null) throw(error);
    }
    
    var create_time = lua.os.time();
    {
        var data = g_HeadImageManager.cache.GetData(msg.account_id);
        if (data != null && data.create_time == create_time)
            throw("已经有头像正在上传");
        
        var file_path = g_HeadImageManager.GetImagePath(msg.account_id, create_time);
        var file_dir = ALittle.File_GetFilePathByPath(file_path);
        ALittle.File_MakeDeepDir(file_dir);
        error = client.StartReceiveFile(file_path, 0);
        if (error != null) throw(error);
    }
    
    // 检查之前是否已经存在，那么就添加到删除表中
    var data = g_HeadImageManager.cache.GetData(msg.account_id);
    if (data != null)
    {
        g_HeadImageManager.cache.DeleteData(data.id);
        
        var delete = new HeadImageInfoDelete();
        delete.account_id = data.id;
        delete.create_time = data.create_time;
        error = A_MysqlSystem.InsertInto(delete);
        if (error != null) ALittle.Error(error);
    }
    
    var new_data = new HeadImageInfo();
    new_data.id = msg.account_id;
    new_data.create_time = create_time;
    new_data.image_md5 = msg.target_image_md5;
    g_HeadImageManager.cache.CreateData(new_data);
}