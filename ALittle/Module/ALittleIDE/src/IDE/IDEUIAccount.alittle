
namespace ALittleIDE;

protected var g_IDEWebLoginManager:AUIPlugin.AUIWebLoginManager;

protected class IDEUIAccount : ALittle.DisplayLayout
{
	// 账号登录相关
	private ALittle.Text _account_login;
	private ALittle.DropDown _account_dropdown;
	private ALittle.DisplayObject _account_icon;

	public fun TCtor()
	{
		// 初始化登录
		var logingate_ip = g_IDEServerConfig.GetConfig("logingate_ip", "139.159.176.119");
		var logingate_port = g_IDEServerConfig.GetConfig("logingate_port", 1000);
		
		g_IDEWebLoginManager = new AUIPlugin.AUIWebLoginManager();
		g_IDEWebLoginManager.AddEventListener(this, this.HandleAccountInReconnect);
		g_IDEWebLoginManager.AddEventListener(this, this.HandleAccountInLogout);
		g_IDEWebLoginManager.AddEventListener(this, this.HandleAccountInLogin);
		g_IDEWebLoginManager.Setup(logingate_ip, logingate_port, g_IDEConfig);
	}
	
	public fun Shutdown()
	{
		g_IDEWebLoginManager.Shutdown();
	}

	////////////////////////////////////////////////////////////////////////////////////////
	// 处理账号登录
	private fun HandleAccountLoginLB(ALittle.UILButtonDownEvent event)
	{
		g_IDEWebLoginManager.ShowLoginDialog();
	}
	// 已登录
	private fun HandleAccountInLogin(AUIPlugin.AUIWebAccountLoginEvent event)
	{
		this._account_login.visible = false;
		this._account_dropdown.visible = true;

		var data_list = new List<string>();
		data_list[1] = "修改密码";
		data_list[2] = "登出";
		this._account_dropdown.data_list = data_list;
		this._account_dropdown.text = g_IDEWebLoginManager.account_name;
	
		this._account_icon.x_value = this._account_dropdown.x_value + this._account_dropdown.width + 5;
	}
	// 已登出
	private fun HandleAccountInLogout(AUIPlugin.AUIWebAccountLogoutEvent event)
	{
		this._account_login.text = "请登录";
		this._account_login.underline = true;
		this._account_login.visible = true;
		this._account_login.disabled = false;
		this._account_dropdown.visible = false;
	
		this._account_icon.x_value = this._account_login.x_value + this._account_login.width + 5;
	}
	// 正在重连
	private fun HandleAccountInReconnect(AUIPlugin.AUIWebAccountReconnectEvent event)
	{
		this._account_login.text = "正在重连";
		this._account_login.underline = false;
		this._account_login.visible = true;
		this._account_login.disabled = true;
		this._account_dropdown.visible = false;
	
		this._account_icon.x_value = this._account_login.x_value + this._account_login.width + 5;
	}
	
	private fun HandleAccountDropdownChange(ALittle.UISelectChangedEvent event)
	{
		if (event.target.text == "登出")
			g_IDEWebLoginManager.Logout();
		elseif (event.target.text == "修改密码")
			g_IDEWebLoginManager.ShowPasswordDialog();
		
		this._account_dropdown.text = g_IDEWebLoginManager.account_name;
	}
}