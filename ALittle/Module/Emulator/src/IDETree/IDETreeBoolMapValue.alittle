
namespace Emulator;

public class IDETreeBoolMapValue : IDETreeLogic
{
	private IDETreeMap _parent;

	private int _value_cpp_type;

	private ALittle.DropDown _key_dropdown;
	private ALittle.ImageInput _value_input;
	private ALittle.TextButton _insert_button;
	private ALittle.TextButton _delete_button;

	private lua.protobuf_message _msg;
	private lua.protobuf_reflection _rflct;
	private lua.protobuf_field_descriptor _key_field;
	private lua.protobuf_field_descriptor _value_field;

	public ctor(ALittle.ControlSystem ctrl_sys, RootInfo root, IDETreeMap parent, lua.protobuf_reflection rflct, lua.protobuf_message msg, lua.protobuf_field_descriptor key_field, lua.protobuf_field_descriptor value_field)
	{
		this._parent = parent;
		this._rflct = rflct;
		this._msg = msg;
		this._key_field = key_field;
		this._value_field = value_field;
		this._value_cpp_type = lua.protobuf.fielddescriptor_cpptype(value_field);

		var key = lua.protobuf.reflection_getbool(this._rflct, this._msg, this._key_field);
		var value = this.RefreshValue();

		this._item = ctrl_sys.CreateControl{ALittle.DisplayObject}("ide_common_tree_bool_map_value", this);
		this.AddChild(this._item);
		this.width = this._item.width;
		this.height = this._item.height;
		
		this._item_title.text = "["..lua.protobuf.fielddescriptor_cpptypename(key_field).." "..lua.protobuf.fielddescriptor_cpptypename(value_field).."] : ";
		if (key) this._key_dropdown.text = "true";
		else this._key_dropdown.text = "false";
		this._value_input.text = ALittle.String_ToString(value);

		this._key_dropdown.x = this._item_title.width + this._item_title.x + 1;
		this._value_input.x = this._key_dropdown.width + this._key_dropdown.x + 1;
		this._insert_button.x = this._value_input.x + this._value_input.width + 1;
		this._delete_button.x = this._insert_button.x + this._insert_button.width + 1;
		this._item.width = this._delete_button.x + this._delete_button.width + 1;
		this.width = this._item.width;
	}
	
	private fun RefreshValue() : any
	{
		if (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_INT32)
			return lua.protobuf.reflection_getint32(this._rflct, this._msg, this._value_field);
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_UINT32)
			return lua.protobuf.reflection_getuint32(this._rflct, this._msg, this._value_field);
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_INT64)
			return lua.protobuf.reflection_getint64(this._rflct, this._msg, this._value_field);
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_UINT64)
			return lua.protobuf.reflection_getuint64(this._rflct, this._msg, this._value_field);
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_DOUBLE)
			return lua.protobuf.reflection_getdouble(this._rflct, this._msg, this._value_field);
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_FLOAT)
			return lua.protobuf.reflection_getfloat(this._rflct, this._msg, this._value_field);
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_STRING)
			return lua.protobuf.reflection_getstring(this._rflct, this._msg, this._value_field);
		return null;
    }

	public fun HandleKeySelectChanegd(ALittle.UISelectChangedEvent event)
	{
        if (this._key_dropdown.text == "true")
			lua.protobuf.reflection_setbool(this._rflct, this._msg, this._key_field, true);
		else
			lua.protobuf.reflection_setbool(this._rflct, this._msg, this._key_field, false);
		this.Save();
    }

	public fun HandleValueFocusOut(ALittle.UIFocusOutEvent event)
	{
        var text = this._value_input.text;
		
		if (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_INT32)
			lua.protobuf.reflection_setint32(this._rflct, this._msg, this._value_field, ALittle.Math_ToIntOrZero(text));
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_UINT32)
			lua.protobuf.reflection_setuint32(this._rflct, this._msg, this._value_field, ALittle.Math_ToIntOrZero(text));
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_INT64)
			lua.protobuf.reflection_setint64(this._rflct, this._msg, this._value_field, ALittle.Math_ToIntOrZero(text));
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_UINT64)
			lua.protobuf.reflection_setuint64(this._rflct, this._msg, this._value_field, ALittle.Math_ToIntOrZero(text));
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_DOUBLE)
			lua.protobuf.reflection_setdouble(this._rflct, this._msg, this._value_field, ALittle.Math_ToDoubleOrZero(text));
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_FLOAT)
			lua.protobuf.reflection_setfloat(this._rflct, this._msg, this._value_field, ALittle.Math_ToDoubleOrZero(text));
		elseif (this._value_cpp_type == lua.protobuf_cpptype.CPPTYPE_STRING)
			lua.protobuf.reflection_setstring(this._rflct, this._msg, this._value_field, text);
		
		var value = this.RefreshValue();
		this._value_input.text = ALittle.String_ToString(value);
		this.Save();
    }

	public fun HandleInsertClick(ALittle.UIClickEvent event)
	{
		this._parent.CreateOneBefore(this);
    }
	
	public fun HandleDeleteClick(ALittle.UIClickEvent event)
	{
		this._parent.Delete(this);
    }
}