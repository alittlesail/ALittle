
namespace Emulator;

public class IDETreeValueMapEnum : IDETreeLogic
{
	private IDETreeMap _parent;

	private ALittle.ImageInput _key_input;
	private ALittle.DropDown _value_dropdown;
	private ALittle.TextButton _insert_button;
	private ALittle.TextButton _delete_button;
	
	private int _key_cpp_type;
	private lua.protobuf_message _msg;
	private lua.protobuf_reflection _rflct;
	private lua.protobuf_field_descriptor _key_field;
	private lua.protobuf_field_descriptor _value_field;
	
	private Map<string, int> _enum_value_map;

	public ctor(ALittle.ControlSystem ctrl_sys, RootInfo root, IDETreeMap parent, lua.protobuf_reflection rflct, lua.protobuf_message msg, lua.protobuf_field_descriptor key_field, lua.protobuf_field_descriptor value_field)
	{
		this._parent = parent;
		this._rflct = rflct;
		this._msg = msg;
		this._key_field = key_field;
		this._value_field = value_field;
		
		this._key_cpp_type = lua.protobuf.fielddescriptor_cpptype(key_field);
		var key = this.RefreshValue();
		var value = lua.protobuf.reflection_getint32(this._rflct, this._msg, this._value_field);
		var enum_descriptor = lua.protobuf.fielddescriptor_enumtype(this._value_field);
		var value_count = lua.protobuf.enumdescriptor_valuecount(enum_descriptor);
		var data_list = new List<string>();
		this._enum_value_map = new Map<string, int>();
		var value_string = "";
		for (var i = 0; i < value_count; 1)
		{
			var enum_value = lua.protobuf.enumdescriptor_value(enum_descriptor, i);
            data_list[i + 1] = lua.protobuf.enumvaluedescriptor_name(enum_value);
			var number = lua.protobuf.enumvaluedescriptor_number(enum_value);
			this._enum_value_map[data_list[i + 1]] = number;
			if (value == number) value_string = data_list[i + 1];
        }

		this._item = ctrl_sys.CreateControl{ALittle.DisplayObject}("ide_common_tree_value_map_enum", this);
		this.AddChild(this._item);
		this.width = this._item.width;
		this.height = this._item.height;
		
		this._item_title.text = "["..lua.protobuf.fielddescriptor_cpptypename(key_field).." "..lua.protobuf.fielddescriptor_cpptypename(value_field).."] : ";
		this._key_input.text = ALittle.String_ToString(key);
		
		this._value_dropdown.data_list = data_list;
		this._value_dropdown.text = value_string;
		this._key_input.x = this._item_title.width + this._item_title.x + 1;
		this._value_dropdown.x = this._key_input.width + this._key_input.x + 1;
		this._insert_button.x = this._value_dropdown.x + this._value_dropdown.width + 1;
		this._delete_button.x = this._insert_button.x + this._insert_button.width + 1;
		this._item.width = this._delete_button.x + this._delete_button.width + 1;
		this.width = this._item.width;
	}
	
	private fun RefreshValue() : any
	{
		if (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_INT32)
			return lua.protobuf.reflection_getint32(this._rflct, this._msg, this._key_field);
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_UINT32)
			return lua.protobuf.reflection_getuint32(this._rflct, this._msg, this._key_field);
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_INT64)
			return lua.protobuf.reflection_getint64(this._rflct, this._msg, this._key_field);
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_UINT64)
			return lua.protobuf.reflection_getuint64(this._rflct, this._msg, this._key_field);
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_DOUBLE)
			return lua.protobuf.reflection_getdouble(this._rflct, this._msg, this._key_field);
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_FLOAT)
			return lua.protobuf.reflection_getfloat(this._rflct, this._msg, this._key_field);
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_STRING)
			return lua.protobuf.reflection_getstring(this._rflct, this._msg, this._key_field);
		return null;
    }

	public fun HandleKeyInputFocusOut(ALittle.UIFocusOutEvent event)
	{
        var text = this._key_input.text;
		
		if (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_INT32)
			lua.protobuf.reflection_setint32(this._rflct, this._msg, this._key_field, ALittle.Math_ToIntOrZero(text));
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_UINT32)
			lua.protobuf.reflection_setuint32(this._rflct, this._msg, this._key_field, ALittle.Math_ToIntOrZero(text));
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_INT64)
			lua.protobuf.reflection_setint64(this._rflct, this._msg, this._key_field, ALittle.Math_ToIntOrZero(text));
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_UINT64)
			lua.protobuf.reflection_setuint64(this._rflct, this._msg, this._key_field, ALittle.Math_ToIntOrZero(text));
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_DOUBLE)
			lua.protobuf.reflection_setdouble(this._rflct, this._msg, this._key_field, ALittle.Math_ToDoubleOrZero(text));
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_FLOAT)
			lua.protobuf.reflection_setfloat(this._rflct, this._msg, this._key_field, ALittle.Math_ToDoubleOrZero(text));
		elseif (this._key_cpp_type == lua.protobuf_cpptype.CPPTYPE_STRING)
			lua.protobuf.reflection_setstring(this._rflct, this._msg, this._key_field, text);
		
		var value = this.RefreshValue();
		this._key_input.text = ALittle.String_ToString(value);
		this.Save();
    }

	public fun HandleValueSelectChanegd(ALittle.UISelectChangedEvent event)
	{
		var value = this._enum_value_map[event.target.text];
		if (value == null) return;

		var index = this._parent.GetChildIndex(this) - 1;
		lua.protobuf.reflection_setint32(this._rflct, this._msg, this._value_field, value);
		this.Save();
    }

	public fun HandleInsertClick(ALittle.UIClickEvent event)
	{
		this._parent.CreateOneBefore(this);
    }
	
	public fun HandleDeleteClick(ALittle.UIClickEvent event)
	{
		this._parent.Delete(this);
    }
}