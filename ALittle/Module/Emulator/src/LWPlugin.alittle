
namespace alittle;

///////////////////////////////////////////////////////////
protected struct NetAddress
{
    string domain_name;        // 域名
    string ip;        // ip
    int port;        // 端口
}

protected struct MsgRegServerInfo
{
    int server_type;        // 服务器的类型
    int server_id;        // 服务器的id
	int kingdom_id;        // 王国ID
    NetAddress net_address;        // 地址
}

protected struct MSG_SERVER_REGIST_REQ
{
    MsgRegServerInfo info;
}

protected struct MSG_LS2CL_LOGIN_RSP
{
    long player_id;
    string login_session;
    NetAddress net_addr;
    int ret_code;
}

protected struct ClientVersion
{
    int major;
    int minor;
    int patch;
}

protected struct MSG_CL2GS_LOGIN_REQ
{
    long player_id;
    string login_session;
    string account;
    ClientVersion client_version;        // Current client version
    int protocol_version;        // 通f定版本
}

protected struct MSG_GS2CL_LOGIN_RSP
{
    int ret_code;
    int flag;        // 第三方充值入口，0 关闭，非0 显示
    long player_id;
    int kingdom_id;
}

protected struct MSG_CLIENT_KEEP_LIVE_REQ
{
}

// 客户端心跳应答包
protected struct MSG_CLIENT_KEEP_LIVE_RSP
{
}

///////////////////////////////////////////////////////////////

// id映射protobuf中message的fullname
private var g_id_map_name = new Map<int, string>();
// fullname映射id
private var g_name_map_id = new Map<string, int>();

// proto被重新刷新
public static __PLUGIN_ProtoRefresh()
{
    var descriptor = A_LuaSocketSchedule.GetEnumDescriptor("EMsgTypes");
	if (descriptor == null) return;
    
    var count = lua.protobuf.enumdescriptor_valuecount(descriptor);
	for (var i = 0; i < count; 1)
	{
		var enumvalue_descriptor = lua.protobuf.enumdescriptor_value(descriptor, i);
		if (enumvalue_descriptor != null)
		{
			var field_name = lua.protobuf.enumvaluedescriptor_name(enumvalue_descriptor);
			var field_number = lua.protobuf.enumvaluedescriptor_number(enumvalue_descriptor);

			var msg_name = ALittle.String_Sub(field_name, 2);
			var full_name = "ProtoMsg."..msg_name;

			g_id_map_name[field_number] = full_name;
			g_name_map_id[full_name] = field_number;
		}
	}
}

// 开始执行登陆流程【在协程内调用】

public await static __PLUGIN_StartLogin(string ip, int port, lua.protobuf_message login_msg) : string, Lua.ISocket
{
	var error:string = null;

	var ls_socket = new Emulator.PluginSocket();
	{
		error = ls_socket.Connect(ip, port);
		if (error != null) return error, null;
    }

	// 发送注册包
	{
		var register_req = new MSG_SERVER_REGIST_REQ();
		register_req.info = new MsgRegServerInfo();
		register_req.info.server_type = 1;
		error = ls_socket.SendStruct("ProtoMsg.MSG_SERVER_REGIST_REQ", register_req);
		if (error != null) return error, null;   
    }
		
	// 发送消息包
	{
		ls_socket.SendMessage(login_msg);
    }

    var gs_socket:Emulator.PluginSocket = null;
	// 读取消息包
	var ls_rsp:MSG_LS2CL_LOGIN_RSP = null;
	{
		error, ls_rsp = ls_socket.ReadStruct{MSG_LS2CL_LOGIN_RSP}();
		if (error != null) return error, null;

		if (ls_rsp.ret_code != null && ls_rsp.ret_code != 0) return "login server login failed, ret code:"..ls_rsp.ret_code, null;
		gs_socket = new Emulator.PluginSocket();
		error = gs_socket.Connect(ls_rsp.net_addr.ip, ls_rsp.net_addr.port);
		if (error != null) return error, null;
    }
	ls_socket.Close();
		
	// 发送注册包
	{
		var register_req = new MSG_SERVER_REGIST_REQ();
		register_req.info = new MsgRegServerInfo();
		register_req.info.server_type = 1;
		error = gs_socket.SendStruct("ProtoMsg.MSG_SERVER_REGIST_REQ", register_req);
		if (error != null) return error, null;
    }

	// 发送登录包
	{
        var login_req = new MSG_CL2GS_LOGIN_REQ();
		login_req.player_id = ls_rsp.player_id;
		login_req.account = ALittle.String_ToString(ls_rsp.player_id);
		login_req.login_session = ls_rsp.login_session;
		error = gs_socket.SendStruct("ProtoMsg.MSG_CL2GS_LOGIN_REQ", login_req);
		if (error != null) return error, null;
    }

	// 处理登陆应答包
	var gs_rsp:MSG_GS2CL_LOGIN_RSP = null;
	{
		error, gs_rsp = gs_socket.ReadStruct{MSG_GS2CL_LOGIN_RSP}();
		if (error != null) return error, null;

		if (gs_rsp.ret_code != null && gs_rsp.ret_code != 0) return "game server login failed, ret code:"..gs_rsp.ret_code, null;
    }

	return null, gs_socket;
}


////////////////////////////////////////////////////////////////////////////////////////////////////
// Socket 部分
////////////////////////////////////////////////////////////////////////////////////////////////////

// 根据项目的消息结构来读取一个消息包
// param socket[Lua.ISocket] 消息对象
// return error[string] 如果读取错误，那么就返回错误原因，如果正确那么就返回null
// return msg[lua.protobuf_message] 如果读取正确，返回protobuf消息对象
public await static __SOCKET_ReadMessage(Lua.ISocket socket) : string, lua.protobuf_message
{
    // 定义接收变量
    var msg_type = 0;
    var msg_size = 0;
    var server_id = 0;
    var server_type = 0;
    var protobuf_msg:lua.protobuf_message = null;
    var error:string = null;
    
    // 接收数据包大小
    error, msg_size = socket.ReadUint16();
    if (error != null) return error, null;
    // 扣除消息头的8个字节，表示消息体的大小
    msg_size -= 8;
    
    // 读取消息类型
    error, msg_type = socket.ReadUint16();
    if (error != null) return error, null;
    
    // 读取服务器ID
    error, server_id = socket.ReadUint16();
    if (error != null) return error, null;
    
    // 读取服务器类型
    error, server_type = socket.ReadUint16();
    if (error != null) return error, null;
    
    // 根据消息类型获取对应的protobuf全称
    var full_name = g_id_map_name[msg_type];

    // 如果消息小于或等于0，那么就根据msg_type来构建一个空的protobuf消息对象
    if (msg_size <= 0)
    {
        if (full_name == null) return "unknow msg type:"..msg_type, null;
        return null, A_LuaSocketSchedule.CreateMessage(full_name);
    }
    // 否则根据大小来读取protobuf消息对象
    else
    {
        return socket.ReadProtobuf(full_name, msg_size);
    }   
}

// 根据项目的消息结构来发送一个消息包
public static __SOCKET_WriteMessage(Lua.ISocket socket, string full_name, lua.protobuf_message protobuf_msg, any protobuf_binary, int protobuf_size) : string
{
    // 根据protobuf消息全称来获取消息类型
    var msg_type = g_name_map_id[full_name];
    if (msg_type == null) return "MsgFullName2MsgId failed, full_name:"..full_name;

    // 发送消息大小
    socket.WriteUint16(protobuf_size + 8);
    // 发送消息类型
    socket.WriteUint16(msg_type);
    // 发送服务器ID
    socket.WriteUint16(1);
    // 发送服务器类型
    socket.WriteUint16(1);
    // 发送protobuf二进制数据
    socket.WriteBinary(protobuf_binary, protobuf_size);
    
    return null;
}

public static __SOCKET_HandleMessage(Lua.ISocket socket, lua.protobuf_message msg)
{
    // 如果是心跳包，那么就要应答
    var descriptor = lua.protobuf.message_getdescriptor(msg);
    var name = lua.protobuf.messagedescriptor_name(descriptor);
    if (name == "MSG_CLIENT_KEEP_LIVE_REQ")
    {
        socket.SendStruct("ProtoMsg.MSG_CLIENT_KEEP_LIVE_RSP", new MSG_CLIENT_KEEP_LIVE_RSP());
        return;
    }
}