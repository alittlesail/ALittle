
[Language(JavaScript)]
namespace JavaScript;

public static RGBToHex(int r, int g, int b) : string
{
    var hex = cast<javascript.Number>((r * 65536 + g * 256 + b)).toString(16);
    if (cast<javascript.String>(hex).length <= 0) return "#000000";
    if (cast<javascript.String>(hex).length <= 1) return "#00000" .. hex;
    if (cast<javascript.String>(hex).length <= 2) return "#0000" .. hex;
    if (cast<javascript.String>(hex).length <= 3) return "#000" .. hex;
    if (cast<javascript.String>(hex).length <= 4) return "#00" .. hex;
    if (cast<javascript.String>(hex).length <= 5) return "#0" .. hex;
    return "#"..hex;
}

public static RGBCombine(int r, int g, int b) : int
{
    return r * 65536 + g * 256 + b;
}

public class JDisplayObject : ALittle.IDisplayObject
{
    private PIXI.DisplayObject _native;
    private PIXI.Point _scale;
    private PIXI.Point _pivot;
    private bool _clip;
    private bool _visible;
    private double _x;
    private double _y;
    
    public ctor()
    {
        this._scale = new PIXI.Point(1, 1);
        this._pivot = new PIXI.Point(0, 0);
        this._clip = false;
        this._visible = true;
        this._x = 0;
        this._y = 0;
    }
    
    public get native() : PIXI.DisplayObject { return this._native; }
    public fun SetX(double x) { this._x = x; this._native.x = this._x; }
    public fun SetY(double y) { this._y = y; this._native.y = this._y; }
    public fun SetScaleX(double value) { this._scale.x = value; this._native.scale = this._scale; }
    public fun SetScaleY(double value) { this._scale.y = value; this._native.scale = this._scale; }
    public fun SetCenterX(double value) { this._pivot.x = value; this._native.pivot = this._pivot; }
    public fun SetCenterY(double value) { this._pivot.y = value; this._native.pivot = this._pivot; }
    public fun SetAngle(double value) { this._native.angle = value; }
    public fun SetWidth(double value) { }
    public fun SetHeight(double value) { }
    public fun SetVisible(bool value) { this._visible = value; this._native.visible = value && !this._clip; }
    public fun SetClip(bool value) { this._clip = value; this._native.visible = !value && this._visible; }
    public fun SetAlpha(double value) { this._native.alpha = value; }
    public fun SetRed(double value) {}
    public fun SetGreen(double value) {}
    public fun SetBlue(double value) {}
    public fun RemoveChild(ALittle.IDisplayObject value) {}
    public fun AddChild(ALittle.IDisplayObject value) {}
    public fun AddChildBefore(ALittle.IDisplayObject value1, ALittle.IDisplayObject value2) {}
    public fun RemoveAllChild() {}
    public fun SetFont(string path, int size) {}
    public fun ClearTexture() {}
    public fun SetTexture(ALittle.ITexture texture) {}
    public fun SetTextureCoord(double t, double b, double l, double r) {}
    public fun SetFlip(int value) {}
    public fun SetRowColCount(int row_count, int col_count) {}
    public fun SetRowColIndex(int row, int col) {}
    public fun NeedDraw() {}
    public fun SetText(string value) {}
    public fun SetBold(bool value) {}
    public fun SetItalic(bool value) {}
    public fun SetUnderline(bool value) {}
    public fun SetDeleteline(bool value) {}
    public fun GetRealWidth() : int { return 0; }
    public fun GetRealHeight() : int { return 0; }
    public fun GetFontHeight() : int { return 0; }
    public fun SetHAlign(int value) {}
    public fun SetVAlign(int value) {}
    public fun SetCursorAlpha(double value) {}
    public fun GetLineWidth(int value) : int { return 0; }
    public fun GetLineCount() : int { return 0; }
    public fun GetScrollOffset() : int { return 0; }
    public fun IsDefaultText() : bool { return false; }
    public fun SetDefaultText(bool defaultv, string value) {}
    public fun SetDefaultTextAlpha(double value) {}
    public fun GetCursorX() : int { return 0; }
    public fun GetCursorY() : int { return 0; }
    public fun GetCursorHeight() : int { return 0; }
    public fun GetText() : string { return null; }
    public fun SetCursorToEnd() {}
    public fun SetCursorToHome() {}
    public fun ShowCursor(bool value) {}
    public fun ClickCursor(double x, double y) {}
    public fun ClickWordCursor(double x, double y) : bool { return false; }
    public fun SelectAll() {}
    public fun GetSelectText() : string { return null; }
    public fun InsertText(string value) : bool { return false; }
    public fun CursorOffsetLR(bool value) {}
    public fun SelectCursorOffsetLR(bool value) {}
    public fun CursorOffsetUD(bool value) {}
    public fun SelectCursorOffsetUD(bool value) {}
    public fun DeleteText(bool value) : bool { return false; }
    public fun DeleteSelectText() : bool { return false; }
    public fun DragCursorBegin() {}
    public fun DragCursor(double x, double y) {}
    public fun SetCursorRed(double value) {}
    public fun SetCursorGreen(double value) {}
    public fun SetCursorBlue(double value) {}
    public fun SetPasswordMode(bool value) {}
    public fun CursorOffset(bool value) {}
    public fun SelectCursorOffset(bool value) {}
    public fun SetTexUV(int index, double u, double v) {}
    public fun SetPosXY(int index, double x, double y) {}
    public fun SetLeftSize(double value) {}
    public fun SetRightSize(double value) {}
    public fun SetTopSize(double value) {}
    public fun SetBottomSize(double value) {}
    public fun CutTextByWidth(int remain_width, string text, int total_width) : int { return 0; }
    public fun ClearCutWidthCache() {}
    public fun GetCutWidthListCount() : int { return 0; }
    public fun GetCutWidthByIndex(int index) : int { return 0; }
    public fun CalcTextWidth(string value) : int { return 0; }    
}

public class JDisplayObjects : JDisplayObject
{
    public ctor()
    {
        this._native = new PIXI.Container();
    }

    public fun RemoveChild(ALittle.IDisplayObject value)
    {
        cast<PIXI.Container>(this._native).removeChild(cast<JDisplayObject>(value)._native);
    }
    public fun AddChild(ALittle.IDisplayObject value) { cast<PIXI.Container>(this._native).addChild(cast<JDisplayObject>(value)._native); }
    public fun AddChildBefore(ALittle.IDisplayObject back, ALittle.IDisplayObject value)
    {
        var index = cast<PIXI.Container>(this._native).getChildAt(cast<JDisplayObject>(value)._native);
        cast<PIXI.Container>(this._native).addChildAt(cast<JDisplayObject>(value)._native, index - 1);
    }
    public fun RemoveAllChild()
    {
        cast<PIXI.Container>(this._native).removeChildren(0);
    }
}

public class JDisplayView : JDisplayObjects
{
    private PIXI.Graphics _graphics;
    private PIXI.Container _container;
    private double _width;
    private double _height;

    public ctor()
    {
        this._container = new PIXI.Container();
        this._graphics = new PIXI.Graphics();
        this._container.mask = this._graphics;
        cast<PIXI.Container>(this._native).addChild(this._graphics);
        cast<PIXI.Container>(this._native).addChild(this._container);
        this._width = 0;
        this._height = 0;
    }

    public fun SetWidth(double width)
    {
        this._width = width;
        this.Draw();
    }

    public fun SetHeight(double height)
    {
        this._height = height;
        this.Draw();
    }
    
    private fun Draw()
    {
        this._graphics.clear();
        this._graphics.beginFill();
        this._graphics.drawRect(0, 0, this._width, this._height);
        this._graphics.endFill();
    }

    public fun RemoveChild(ALittle.IDisplayObject value)
    {
        this._container.removeChild(cast<JDisplayObject>(value)._native);
    }
    public fun AddChild(ALittle.IDisplayObject value) { this._container.addChild(cast<JDisplayObject>(value)._native); }
    public fun AddChildBefore(ALittle.IDisplayObject back, ALittle.IDisplayObject value)
    {
        var index = this._container.getChildAt(cast<JDisplayObject>(value)._native);
        this._container.addChildAt(cast<JDisplayObject>(value)._native, index - 1);
    }
    public fun RemoveAllChild()
    {
        this._container.removeChildren(0);
    }
}

public class JDisplaySystem : ALittle.IDisplaySystem
{
    private PIXI.Container _layer;
    private PIXI.Container _special_layer;
    
    public ctor()
    {
        this._layer = new PIXI.Container();
        this._special_layer = new PIXI.Container();
    }
    
    public fun AddToStage(PIXI.Container stage)
    {
        this._layer.width = stage.width;
        this._layer.height = stage.height;
        this._special_layer.width = stage.width;
        this._special_layer.height = stage.height;
        
        stage.addChild(this._layer);
        stage.addChild(this._special_layer);
    }
    
    public fun AddSpecialChild(ALittle.IDisplayObject value)
    {
        this._special_layer.addChild(cast<JDisplayObject>(value).native);
    }
    public fun AddChild(ALittle.IDisplayObject value)
    {
        this._layer.addChild(cast<JDisplayObject>(value).native);
    }
    public fun AddChildBefore(ALittle.IDisplayObject back, ALittle.IDisplayObject value)
    {
        var index = this._layer.getChildAt(cast<JDisplayObject>(value).native);
        this._layer.addChildAt(cast<JDisplayObject>(value).native, index - 1);
    }
    public fun RemoveChild(ALittle.IDisplayObject value)
    {
        this._layer.removeChild(cast<JDisplayObject>(value).native);
    }
    public fun RemoveAllChild()
    {
        this._layer.removeChildren(0);
        this._special_layer.removeChildren(0);
    }
}

public var A_JDisplaySystem:JDisplaySystem = new JDisplaySystem();

public class JQuad : JDisplayObject
{
    private int _color;
    private int _red;
    private int _green;
    private int _blue;
    private double _alpha;
    private double _width;
    private double _height;
    
    public ctor()
    {
        this._native = new PIXI.Graphics();
        this._red = 255;
        this._green = 255;
        this._blue = 255;
        this._color = this._red * 65536 + this._green * 256 + this._blue;
        this._alpha = 1;
        this._width = 0;
        this._height = 0;
    }
    
    private fun Draw()
    {
        cast<PIXI.Graphics>(this._native).clear();
        cast<PIXI.Graphics>(this._native).beginFill(this._color, this._alpha);
        cast<PIXI.Graphics>(this._native).drawRect(0, 0, this._width, this._height);
        cast<PIXI.Graphics>(this._native).endFill();
    }
    
    public fun SetWidth(double value)
    {
        this._width = value;
        this.Draw();
    }
    public fun SetHeight(double value)
    {
        this._height = value;
        this.Draw();
    }
    
    public fun SetRed(double value)
    {
        this._red = javascript.Math.floor(value * 255);
        this._color = this._red * 65536 + this._green * 256 + this._blue;
        this.Draw();
    }
    
    public fun SetGreen(double value)
    {
        this._green = javascript.Math.floor(value * 255);
        this._color = this._red * 65536 + this._green * 256 + this._blue;
        this.Draw();
    }
    public fun SetBlue(double value)
    {
        this._blue = javascript.Math.floor(value * 255);
        this._color = this._red * 65536 + this._green * 256 + this._blue;
        this.Draw();
    }
    public fun SetAlpha(double value)
    {
        this._alpha = value;
        this.Draw();
    }   
}

public class JImage : JDisplayObject
{ 
    public ctor()
    {
        this._native = new PIXI.Sprite();
    }

    public fun SetWidth(double width) { this._native.width = width; }
    public fun SetHeight(double height) { this._native.height = height; }
    
    public fun ClearTexture()
    {
        cast<PIXI.Sprite>(this._native).texture = null;
    }
    public fun SetTexture(ALittle.ITexture texture)
    {
        cast<PIXI.Sprite>(this._native).texture = cast<JTexture>(texture).native;
    }
    public fun SetTextureCoord(double t, double b, double l, double r)
    {
        // 因为Web版本的图片都是散装的，所以不需要处理
    }
}

public class JGrid9Image : JDisplayObject
{
    private double _leftWidth;
    private double _rightWidth;
    private double _topHeight;
    private double _bottomHeight;
    private double _width;
    private double _height;

    private PIXI.NineSlicePlane _nine;

    public ctor()
    {
        this._native = new PIXI.Container();
        this._leftWidth = 0;
        this._rightWidth = 0;
        this._topHeight = 0;
        this._bottomHeight = 0;
        this._width = 0;
        this._height = 0;
    }
    
    public fun ClearTexture()
    {
        cast<PIXI.Container>(this._native).removeChildren(0);
        this._nine = null;
    }
    public fun SetTexture(ALittle.ITexture texture)
    {
        if (this._nine == null)
        {
            this._nine = new PIXI.NineSlicePlane(cast<JTexture>(texture).native
                            , this._leftWidth, this._topHeight, this._rightWidth, this._bottomHeight);
            cast<PIXI.Container>(this._native).addChild(this._nine);
            this._nine.width = this._width;
            this._nine.height = this._height;
        }
        else
        {
            cast<PIXI.NineSlicePlane>(this._native).texture = cast<JTexture>(texture).native;
        }
    }
    public fun SetTextureCoord(double t, double b, double l, double r)
    {
        // 因为Web版本的图片都是散装的，所以不需要处理
    }

    public fun SetWidth(double width)
    {
        this._width = width;
        if (this._nine != null) this._nine.width = width;
    }

    public fun SetHeight(double height)
    {
        this._height = height;
        if (this._nine != null) this._nine.height = height;
    }

    public fun SetLeftSize(double value)
    {
        this._leftWidth = value;
        if (this._nine != null) this._nine.leftWidth = value;
    }
    public fun SetRightSize(double value)
    {
        this._rightWidth = value;
        if (this._nine != null) this._nine.rightWidth = value;
    }
    public fun SetTopSize(double value)
    {
        this._topHeight = value;
        if (this._nine != null) this._nine.topHeight = value;
    }
    public fun SetBottomSize(double value)
    {
        this._bottomHeight = value;
        if (this._nine != null) this._nine.bottomHeight = value;
    }
}

public class JSprite : JDisplayObject
{
    private int _row_count;
    private int _col_count;
    private int _row;
    private int _col;
    private int _tex_width;
    private int _tex_height;

    private PIXI.Texture _texture;

    public ctor()
    {
        this._row_count = 1;
        this._col_count = 1;
        this._row = 0;
        this._col = 0;
        this._tex_width = 0;
        this._tex_height = 0;
        this._native = new PIXI.Sprite();
    }

    public fun SetWidth(double width) { this._native.width = width; }
    public fun SetHeight(double height) { this._native.height = height; }
    
    public fun ClearTexture()
    {
        this._texture = null;
        this._tex_width = 0;
        this._tex_height = 0;
        cast<PIXI.Sprite>(this._native).texture = null;
    }
    public fun SetTexture(ALittle.ITexture texture)
    {
        this._tex_width = texture.GetWidth();
        this._tex_height = texture.GetHeight();
        var tile_width = this._tex_width / this._col_count;
        var tile_height = this._tex_height / this._row_count;
        
        var row = this._row;
        var col = this._col;
        if (row < 0 || row >= this._row_count) row = 0;
        if (col < 0 || col >= this._col_count) col = 0;

        var frame = new PIXI.Rectangle(col*tile_width, row*tile_height, tile_width, tile_height);
        this._texture = new PIXI.Texture(cast<JTexture>(texture).native.baseTexture, frame
                            , cast<JTexture>(texture).native.orig, cast<JTexture>(texture).native.trim
                            , cast<JTexture>(texture).native.rotate);
        cast<PIXI.Sprite>(this._native).texture = this._texture;
    }
    public fun SetTextureCoord(double t, double b, double l, double r)
    {
        // 因为Web版本的图片都是散装的，所以不需要处理
    }

    public fun SetRowColCount(int row_count, int col_count)
    {
        this._row_count = row_count;
        if (this._row_count < 1) this._row_count = 1;
        this._col_count = col_count;
        if (this._col_count < 1) this._col_count = 1;
        
        this.UpdateFrame();
    }
    public fun SetRowColIndex(int row, int col)
    {
        this._row = row - 1;
        this._col = col - 1;

        this.UpdateFrame();
    }

    private fun UpdateFrame()
    {
        if (this._texture == null) return;

        var tile_width = this._tex_width / this._col_count;
        var tile_height = this._tex_height / this._row_count;

        var row = this._row;
        var col = this._col;
        if (row < 0 || row >= this._row_count) row = 0;
        if (col < 0 || col >= this._col_count) col = 0;
        this._texture.frame = new PIXI.Rectangle(col*tile_width, row*tile_height, tile_width, tile_height);
    }
}

public class JTriangle : JDisplayObject
{
    private PIXI.SimpleMesh _mesh;

    [Native]
    private List<double> _uv;
    [Native]
    private List<double> _xy;
    [Native]
    private List<int> _index;

    public ctor()
    {
        this._native = new PIXI.Container();
        this._uv = new List<double>();
        for (var i = 0; i < 6; 1) this._uv[i] = 0;
        this._xy = new List<double>();
        for (var i = 0; i < 6; 1) this._xy[i] = 0;
        this._index = new List<int>();
        for (var i = 0; i < 3; 1) this._index[i] = i;
    }
    
    public fun ClearTexture()
    {
        if (this._mesh != null)
            this._mesh.texture = null;
    }
    public fun SetTexture(ALittle.ITexture texture)
    {
        if (this._mesh == null)
        {
            this._mesh = new PIXI.SimpleMesh(cast<JTexture>(texture).native, this._xy, this._uv, this._index);
            cast<PIXI.Container>(this._native).addChild(this._mesh);
        }
        else
        {
            this._mesh.texture = cast<JTexture>(texture).native;
        }
    }
    public fun SetTextureCoord(double t, double b, double l, double r)
    {
        // 因为Web版本的图片都是散装的，所以不需要处理
    }
    
    public fun SetTexUV(int index, double u, double v)
    {
        if (this._mesh == null)
        {
            this._uv[index*2] = u;
            this._uv[index*2 + 1] = v;
        }
        else
        {
            this._mesh.uvBuffer.data[index * 2 + 1] = u;
            this._mesh.uvBuffer.data[(index + 1) * 2] = v;
        }
    }
    public fun SetPosXY(int index, double x, double y)
    {
        if (this._mesh == null)
        {
            this._xy[index*2] = x;
            this._xy[index*2 + 1] = y;
        }
        else
        {
            this._mesh.verticesBuffer.data[index * 2 + 1] = x;
            this._mesh.verticesBuffer.data[(index + 1) * 2] = y;
        }
    }
}

public class JVertexImage : JDisplayObject
{
    private PIXI.SimpleMesh _mesh;

    [Native]
    private List<double> _uv;
    [Native]
    private List<double> _xy;
    [Native]
    private List<int> _index;

    public ctor()
    {
        this._native = new PIXI.Container();
        this._uv = new List<double>();
        for (var i = 0; i < 8; 1) this._uv[i] = 0;
        this._xy = new List<double>();
        for (var i = 0; i < 8; 1) this._xy[i] = 0;
        this._index = new List<int>();
        for (var i = 0; i < 4; 1) this._index[i] = i;
    }

    public fun ClearTexture()
    {
        if (this._mesh != null)
            this._mesh.texture = null;
    }
    public fun SetTexture(ALittle.ITexture texture)
    {
        if (this._mesh == null)
        {
            this._mesh = new PIXI.SimpleMesh(cast<JTexture>(texture).native, this._xy, this._uv, this._index, PIXI.DRAW_MODES.TRIANGLE_FAN);
            cast<PIXI.Container>(this._native).addChild(this._mesh);
        }
        else
        {
            this._mesh.texture = cast<JTexture>(texture).native;
        }
    }
    public fun SetTextureCoord(double t, double b, double l, double r)
    {
        // 因为Web版本的图片都是散装的，所以不需要处理
    }

    public fun SetTexUV(int index, double u, double v)
    {
        if (this._mesh == null)
        {
            this._uv[index*2] = u;
            this._uv[index*2 + 1] = v;
        }
        else
        {
            this._mesh.uvBuffer.data[index * 2 + 1] = u;
            this._mesh.uvBuffer.data[(index + 1) * 2] = v;
        }
    }
    public fun SetPosXY(int index, double x, double y)
    {
        if (this._mesh == null)
        {
            this._xy[index*2] = x;
            this._xy[index*2 + 1] = y;
        }
        else
        {
            this._mesh.verticesBuffer.data[index * 2 + 1] = x;
            this._mesh.verticesBuffer.data[(index + 1) * 2] = y;
        }
    }
}

public class JText : JDisplayObject
{
    private PIXI.TextStyle _style;
    private int _red;
    private int _green;
    private int _blue;
    private int _real_width;
    private int _real_height;
    private string _text;
    
    public ctor()
    {
        this._native = new PIXI.Text();
        this._style = new PIXI.TextStyle();
        this._real_width = 0;
        this._real_height = 0;
        this._red = 255;
        this._green = 255;
        this._blue = 255;
        this._style.fill = RGBCombine(this._red, this._green, this._blue);
        this._text = "";
    }
    
    public fun SetBold(bool value)
    {
        if (value)
            this._style.fontWeight = "bold";
        else
            this._style.fontWeight = "normal";
        cast<PIXI.Text>(this._native).style = this._style;
    }

    public fun SetItalic(bool value)
    {
        if (value)
            this._style.fontStyle = "italic";
        else
            this._style.fontStyle = "normal";   
        cast<PIXI.Text>(this._native).style = this._style;
    }

    public fun SetText(string value)
    {
        this._text = value;
        cast<PIXI.Text>(this._native).text = value;
        var m = PIXI.TextMetrics.measureText(this._text, this._style);
        this._real_width = m.width;
        this._real_height = m.height;
    }
        
    public fun SetFont(string path, int size)
    {
        this._style.fontFamily = path;
        this._style.fontSize = size;
        cast<PIXI.Text>(this._native).style = this._style;
        var m = PIXI.TextMetrics.measureText(this._text, this._style);
        this._real_width = m.width;
        this._real_height = m.height;
    }
    
    public fun SetRed(double value)
    {
        this._red = javascript.Math.floor(value * 255);
        this._style.fill = RGBCombine(this._red, this._green, this._blue);
        cast<PIXI.Text>(this._native).style = this._style;
    }
    
    public fun SetGreen(double value)
    {
        this._green = javascript.Math.floor(value * 255);
        this._style.fill = RGBCombine(this._red, this._green, this._blue);
        cast<PIXI.Text>(this._native).style = this._style;
    }
    public fun SetBlue(double value)
    {
        this._blue = javascript.Math.floor(value * 255);
        this._style.fill = RGBCombine(this._red, this._green, this._blue);
        cast<PIXI.Text>(this._native).style = this._style;
    }
    
    public fun GetRealWidth() : int { return this._real_width; }
    public fun GetRealHeight() : int { return this._real_height; }

    public fun GetFontHeight() : int { return this._real_height; }
}

public class JTextArea : JDisplayObject
{
    private PIXI.TextStyle _style;
    private int _red;
    private int _green;
    private int _blue;
    private string _text;

    private double _width;
    private double _height;

    private int _real_width;
    private int _real_height;

    private int _h_align;
    private int _v_align;
    
    public ctor()
    {
        this._native = new PIXI.Text();
        this._style = new PIXI.TextStyle();
        this._red = 255;
        this._green = 255;
        this._blue = 255;
        this._style.fill = RGBCombine(this._red, this._green, this._blue);
        this._style.wordWrap = true;
        this._style.breakWords = true;
        this._text = "";

        this._width = 0;
        this._height = 0;
        this._real_width = 0;
        this._real_height = 0;

        this._h_align = 0;
        this._v_align = 0;
    }

    public fun SetBold(bool value)
    {
        if (value)
            this._style.fontWeight = "bold";
        else
            this._style.fontWeight = "normal";
        cast<PIXI.Text>(this._native).style = this._style;
    }

    public fun SetItalic(bool value)
    {
        if (value)
            this._style.fontStyle = "italic";
        else
            this._style.fontStyle = "normal";   
        cast<PIXI.Text>(this._native).style = this._style;
    }

    public fun SetHAlign(int align)
    {
        if (this._h_align == align) return;
        this._h_align = align;
        if (this._h_align == ALittle.UIEnumTypes.HALIGN_LEFT)
            this._style.align = "left";
        elseif (this._h_align == ALittle.UIEnumTypes.HALIGN_CENTER)
            this._style.align = "center";
        else
            this._style.align = "right";
        cast<PIXI.Text>(this._native).style = this._style;
        this.UpdateShow();
    }

    public fun SetVAlign(int align)
    {
        if (this._v_align == align) return;
        this._v_align = align;
        this.UpdateShow();
    }

    public fun SetX(double x)
    {
        this._x = x;
        this.UpdateShow();
    }

    public fun SetY(double y)
    {
        this._y = y;
        this.UpdateShow();
    }

    public fun SetWidth(double width)
    {
        this._width = width;
        this._style.wordWrapWidth = javascript.Math.floor(width);
        cast<PIXI.Text>(this._native).style = this._style;
        
        var m = PIXI.TextMetrics.measureText(this._text, this._style);
        this._real_width = m.width;
        this._real_height = m.height;

        this.UpdateShow();
    }

    public fun SetHeight(double height)
    {
        this._height = height;
        this.UpdateShow();
    }
    
    public fun SetText(string value)
    {
        this._text = value;
        cast<PIXI.Text>(this._native).text = value;
        cast<PIXI.Text>(this._native).style = this._style;
        
        var m = PIXI.TextMetrics.measureText(this._text, this._style);
        this._real_width = m.width;
        this._real_height = m.height;
        this.UpdateShow();
    }
        
    public fun SetFont(string path, int size)
    {
        this._style.fontFamily = path;
        this._style.fontSize = size;
        cast<PIXI.Text>(this._native).style = this._style;
        
        var m = PIXI.TextMetrics.measureText(this._text, this._style);
        this._real_width = m.width;
        this._real_height = m.height;
        this.UpdateShow();
    }
    
    public fun SetRed(double value)
    {
        this._red = javascript.Math.floor(value * 255);
        this._style.fill = RGBCombine(this._red, this._green, this._blue);
        cast<PIXI.Text>(this._native).style = this._style;
    }
    
    public fun SetGreen(double value)
    {
        this._green = javascript.Math.floor(value * 255);
        this._style.fill = RGBCombine(this._red, this._green, this._blue);
        cast<PIXI.Text>(this._native).style = this._style;
    }
    public fun SetBlue(double value)
    {
        this._blue = javascript.Math.floor(value * 255);
        this._style.fill = RGBCombine(this._red, this._green, this._blue);
        cast<PIXI.Text>(this._native).style = this._style;
    }

    public fun GetRealHeight() : int
    {
        return this._real_height;
    }

    public fun UpdateShow()
    {
        if (this._h_align == ALittle.UIEnumTypes.HALIGN_LEFT)
            this._native.x = javascript.Math.floor(this._x);
        elseif (this._h_align == ALittle.UIEnumTypes.HALIGN_CENTER)
            this._native.x = javascript.Math.floor((this._width - this._real_width) / 2 + this._x);
        else
            this._native.x = javascript.Math.floor(this._width - this._real_width + this._x);

        if (this._v_align == ALittle.UIEnumTypes.VALIGN_TOP)
            this._native.y = javascript.Math.floor(this._y);
        elseif (this._v_align == ALittle.UIEnumTypes.VALIGN_CENTER)
            this._native.y = javascript.Math.floor((this._height - this._real_height) / 2 + this._y);
        else
            this._native.y = javascript.Math.floor(this._height - this._real_height + this._y);
    }
}

public class JTextInput : JDisplayObject
{
    private bool _is_default_text;
    private string _default_text;
    
    private int _red;
    private int _green;
    private int _blue;

    public ctor()
    {
        this._is_default_text = false;
        this._default_text = "";

        var style = new PIXI.TextInputStyle();
        this._native = new PIXI.TextInput(style);
        cast<PIXI.TextInput>(this._native).disabled = true;

        this.SetRed(1);
        this.SetGreen(1);
        this.SetBlue(1);
    }

    public fun SetDisabled(bool value)
    {
        cast<PIXI.TextInput>(this._native).disabled = value;
    }

    public fun IsDefaultText() : bool { return this._is_default_text; }
    public fun SetDefaultText(bool value, string text)
    {
        this._is_default_text = value;
        this._default_text = text;
        if (this._is_default_text)
            cast<PIXI.TextInput>(this._native).placeholder = text;
        else
            cast<PIXI.TextInput>(this._native).placeholder = "";
    }

    public fun SetPasswordMode(bool value)
    {
        if (value)
            cast<PIXI.TextInput>(this._native).htmlInput.type = "password";
        else
            cast<PIXI.TextInput>(this._native).htmlInput.type = "text";
    }

    public fun SetWidth(double width)
    {
        cast<PIXI.TextInput>(this._native).setInputStyle("width", javascript.Math.floor(width).."px");
    }

    public fun SetBold(bool value)
    {
        if (value)
            cast<PIXI.TextInput>(this._native).setInputStyle("fontWeight", "bold");
        else
            cast<PIXI.TextInput>(this._native).setInputStyle("fontWeight", "normal");
    }

    public fun SetItalic(bool value)
    {
        if (value)
            cast<PIXI.TextInput>(this._native).setInputStyle("fontStyle", "italic");
        else
            cast<PIXI.TextInput>(this._native).setInputStyle("fontStyle", "normal");
    }

    public fun SetText(string value)
    {
        cast<PIXI.TextInput>(this._native).text = value;
    }

    public fun GetText() : string
    {
        return cast<PIXI.TextInput>(this._native).text;
    }
        
    public fun SetFont(string path, int size)
    {
        cast<PIXI.TextInput>(this._native).setInputStyle("fontSize", size .. "px");
        cast<PIXI.TextInput>(this._native).setInputStyle("fontFamily", path);
    }
    
    public fun SetRed(double value)
    {
        this._red = javascript.Math.floor(value * 255);
        cast<PIXI.TextInput>(this._native).setInputStyle("color", RGBToHex(this._red, this._green, this._blue));
    }
    
    public fun SetGreen(double value)
    {
        this._green = javascript.Math.floor(value * 255);
        cast<PIXI.TextInput>(this._native).setInputStyle("color", RGBToHex(this._red, this._green, this._blue));
    }
    public fun SetBlue(double value)
    {
        this._blue = javascript.Math.floor(value * 255);
        cast<PIXI.TextInput>(this._native).setInputStyle("color", RGBToHex(this._red, this._green, this._blue));
    }
}

public class JTextEdit : JDisplayObject
{
    private int _red;
    private int _green;
    private int _blue;

    public ctor()
    {
        var style = new PIXI.TextInputStyle();
        style.input = new PIXI.TextInputStyleInput();
        style.input.multiline = true;
        this._native = new PIXI.TextInput(style);
        cast<PIXI.TextInput>(this._native).disabled = true;
    }

    public fun SetDisabled(bool value)
    {
        cast<PIXI.TextInput>(this._native).disabled = value;
    }

    public fun SetWidth(double width)
    {
        cast<PIXI.TextInput>(this._native).setInputStyle("width", javascript.Math.floor(width).."px");
    }

    public fun SetHeight(double height)
    {
        cast<PIXI.TextInput>(this._native).setInputStyle("height", javascript.Math.floor(height).."px");
    }

    public fun SetBold(bool value)
    {
        if (value)
            cast<PIXI.TextInput>(this._native).setInputStyle("fontWeight", "bold");
        else
            cast<PIXI.TextInput>(this._native).setInputStyle("fontWeight", "normal");
    }

    public fun SetItalic(bool value)
    {
        if (value)
            cast<PIXI.TextInput>(this._native).setInputStyle("fontStyle", "italic");
        else
            cast<PIXI.TextInput>(this._native).setInputStyle("fontStyle", "normal");
    }

    public fun SetText(string value)
    {
        cast<PIXI.TextInput>(this._native).text = value;
    }

    public fun GetText() : string
    {
        return cast<PIXI.TextInput>(this._native).text;
    }
        
    public fun SetFont(string path, int size)
    {
        cast<PIXI.TextInput>(this._native).setInputStyle("fontSize", size .. "px");
        cast<PIXI.TextInput>(this._native).setInputStyle("fontFamily", path);
    }
    
    public fun SetRed(double value)
    {
        this._red = javascript.Math.floor(value * 255);
        cast<PIXI.TextInput>(this._native).setInputStyle("color", RGBToHex(this._red, this._green, this._blue));
    }
    
    public fun SetGreen(double value)
    {
        this._green = javascript.Math.floor(value * 255);
        cast<PIXI.TextInput>(this._native).setInputStyle("color", RGBToHex(this._red, this._green, this._blue));
    }
    public fun SetBlue(double value)
    {
        this._blue = javascript.Math.floor(value * 255);
        cast<PIXI.TextInput>(this._native).setInputStyle("color", RGBToHex(this._red, this._green, this._blue));
    }
}

private var __TEXTURELOADER_MAXID = 0;

public class JTexture : ALittle.ITexture
{
    private PIXI.Texture _texture;
    private int _width;
    private int _height;
    
    public ctor(PIXI.Texture texture, int width, int height)
    {
        this._texture = texture;
        this._width = width;
        this._height = height;
    }
    
    public fun Clear()
    {
        this._texture = null;
    }
    public get native() : PIXI.Texture { return this._texture; }
    public fun GetTexture() : ALittle.ITexture { return this; }
    public fun GetWidth() : int { return this._width; }
    public fun GetHeight() : int { return this._height; }
}

public class JTextureLoader : ALittle.ITextureLoader
{
    private int _id;
    private string _file_path;
    private string _atlas_info;
    private int _width;
    private int _height;
    private bool _load_failed;
    private string _altas;
    
    public ctor()
    {
        ++ __TEXTURELOADER_MAXID;
        this._id = __TEXTURELOADER_MAXID;
        this._load_failed = false;
    }
    
    public fun SetPath(string big_path, string altas, int big_width, int big_height, bool crypt_mode)
    {
        this._file_path = big_path;
        this._atlas_info = altas;
        this._width = big_width;
        this._height = big_height;
        this._altas = altas;
    }
    
    public fun Start()
    {
        var real_path:string = null;
        var altas_list = ALittle.String_Split(this._altas, ";");
        for (var index, altas in altas_list)
        {
            var split = ALittle.String_Split(altas, ",");
            if (split[1] != null)
                real_path = split[1];
        }
        if (real_path == null)
        {
            this.HandleLoadFailed();
            return;
        }
        
        var loader = new PIXI.Loader();
        loader.onError.add(bind(this.HandleLoadFailed, this));
        loader.add(this._file_path, real_path).load(bind(this.HandleLoadSucceed, this));
    }
    public fun GetID() : int { return this._id; }
    public fun GetPath() : string { return this._file_path; }
        
    private fun HandleLoadSucceed(PIXI.Loader loader, Map<string, PIXI.LoaderResource> resources)
    {
        if (this._load_failed) return;
        
        var resource = resources[this._file_path];
        if (resource == null || resource.texture == null)
        {
            this.HandleLoadFailed();
            return;
        }
        
        var func = cast<Map<string, Functor<(ALittle.ITextureLoader, ALittle.ITexture)>>>(window)["__ALITTLEAPI_TextureLoadSucceed"];
        if (func == null) return;
        func(this, new JTexture(resource.texture, cast<int>(resource.texture.orig.width), cast<int>(resource.texture.orig.height)));
    }
       
    private fun HandleLoadFailed()
    {
        if (this._load_failed) return;
        
        this._load_failed = true;
        var func = cast<Map<string, Functor<(ALittle.ITextureLoader)>>>(window)["__ALITTLEAPI_TextureLoadFailed"];
        if (func == null) return;
        func(this);
    }
}

public class JTextureCutLoader : ALittle.ITextureCutLoader
{
    private int _id;
    
    public ctor()
    {
        ++ __TEXTURELOADER_MAXID;
        this._id = __TEXTURELOADER_MAXID;
    }
    
    public fun SetPath(string path, int max_width, int max_height) {}
    public fun Start() {}
    public fun GetID() : int { return this._id; }
    public fun GetPath() : string { return null; }
    public fun GetMaxWidth() : int { return null; }
    public fun GetMaxHeight() : int { return null; }
}