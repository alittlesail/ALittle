
[Language(JavaScript)]
namespace JavaScript;

private var __pixel_ratio = 1;

private var KEY_CODE_MAP = new Map<int, int>();
private var SCAN_CODE_MAP = new Map<int, int>();
KEY_CODE_MAP[65] = 97; SCAN_CODE_MAP[65] = 4;	// A
KEY_CODE_MAP[83] = 115; SCAN_CODE_MAP[83] = 22;	// S
KEY_CODE_MAP[68] = 100; SCAN_CODE_MAP[68] = 7;	// D
KEY_CODE_MAP[87] = 119; SCAN_CODE_MAP[87] = 26;	// W
KEY_CODE_MAP[71] = 103; SCAN_CODE_MAP[71] = 10;	// G
KEY_CODE_MAP[72] = 104; SCAN_CODE_MAP[72] = 11;	// H
KEY_CODE_MAP[74] = 106; SCAN_CODE_MAP[74] = 13;	// J
KEY_CODE_MAP[75] = 107; SCAN_CODE_MAP[75] = 14;	// K

KEY_CODE_MAP[37] = 1073741904; SCAN_CODE_MAP[37] = 80;	// LEFT
KEY_CODE_MAP[40] = 1073741905; SCAN_CODE_MAP[40] = 81;	// DOWN
KEY_CODE_MAP[39] = 1073741903; SCAN_CODE_MAP[39] = 79;	// RIGHT
KEY_CODE_MAP[38] = 1073741906; SCAN_CODE_MAP[38] = 82;	// UP
KEY_CODE_MAP[97] = 1073741913; SCAN_CODE_MAP[97] = 89;	// 1
KEY_CODE_MAP[98] = 1073741914; SCAN_CODE_MAP[98] = 90;	// 2
KEY_CODE_MAP[100] = 1073741916; SCAN_CODE_MAP[100] = 92;	// 4
KEY_CODE_MAP[101] = 1073741917; SCAN_CODE_MAP[101] = 93;	// 5

public static JSystem_GetDeviceID() : string
{
    // 先从cookie获取，如果没有就随机生成一个
    var id:string = null;
    var json:Map<string, any> = null;
    if (document.cookie != null && document.cookie != "")
    {
        var error:string = null;
        error, json = tcall(ALittle.String_JsonDecode, document.cookie);
        if (error != null) json = null;
    }
    if (json != null)
        id = json["device_id"];
    if (id == null)
    {
        var rand = ALittle.Math_RandomInt(0, 10000);
        var time = ALittle.Time_GetCurTime();
        if (json == null) json = new Map<string, any>();
        id = "device_id_"..rand.."_"..time;
        json["device_id"] = id;

        // 微信环境不能操作cookie
        if (window.wx == null) document.cookie = ALittle.String_JsonEncode(json);
    }
    return id;
}

public var A_PixiApp:PIXI.Application;

private static JSystem_FingerDown(javascript.DomElementOnTouchEvent event)
{
    var func = cast<Map<string, Functor<(double, double, int, int)>>>(window)["__ALITTLEAPI_FingerDown"];
    if (func == null) return;
    
	var offsetLeft = 0;
	var offsetTop = 0;
    if (event.srcElement != null)
    {
        offsetLeft = event.srcElement.offsetLeft;
        offsetTop = event.srcElement.offsetTop;
    }

    for (var index, touch in event.changedTouches)
        func((touch.pageX - offsetLeft) / A_PixiApp.stage.scale.x * __pixel_ratio, (touch.pageY - offsetTop) / A_PixiApp.stage.scale.y * __pixel_ratio, touch.identifier, 1);
}

private static JSystem_FingerUp(javascript.DomElementOnTouchEvent event)
{
    var func = cast<Map<string, Functor<(double, double, int, int)>>>(window)["__ALITTLEAPI_FingerUp"];
    if (func == null) return;
    
	var offsetLeft = 0;
	var offsetTop = 0;
    if (event.srcElement != null)
    {
        offsetLeft = event.srcElement.offsetLeft;
        offsetTop = event.srcElement.offsetTop;
    }

    for (var index, touch in event.changedTouches)
    	func((touch.pageX - offsetLeft) / A_PixiApp.stage.scale.x * __pixel_ratio, (touch.pageY - offsetTop) / A_PixiApp.stage.scale.y * __pixel_ratio, touch.identifier, 1);
}

private static JSystem_FingerMoved(javascript.DomElementOnTouchEvent event)
{
    var func = cast<Map<string, Functor<(double, double, int, int)>>>(window)["__ALITTLEAPI_FingerMoved"];
    if (func == null) return;
    
	var offsetLeft = 0;
	var offsetTop = 0;
    if (event.srcElement != null)
    {
        offsetLeft = event.srcElement.offsetLeft;
        offsetTop = event.srcElement.offsetTop;
    }

    for (var index, touch in event.changedTouches)
    	func((touch.pageX - offsetLeft) / A_PixiApp.stage.scale.x * __pixel_ratio, (touch.pageY - offsetTop) / A_PixiApp.stage.scale.y * __pixel_ratio, touch.identifier, 1);
}

private static JSystem_MouseMoved(javascript.DomElementOnMouseEvent event)
{
    var func = cast<Map<string, Functor<(double, double)>>>(window)["__ALITTLEAPI_MouseMoved"];
    if (func == null) return;
    
    func((event.pageX - event.srcElement.offsetLeft) / A_PixiApp.stage.scale.x, (event.pageY - event.srcElement.offsetTop) / A_PixiApp.stage.scale.y);
}

private static JSystem_MouseDown(javascript.DomElementOnMouseEvent event)
{
    var func:Functor<(double, double, int)>;
    if (event.which == 1)
        func = cast<Map<string, Functor<(double, double, int)>>>(window)["__ALITTLEAPI_LButtonDown"];
    elseif (event.which == 2)
        func = cast<Map<string, Functor<(double, double, int)>>>(window)["__ALITTLEAPI_MButtonDown"];
    elseif (event.which == 3)
        func = cast<Map<string, Functor<(double, double, int)>>>(window)["__ALITTLEAPI_RButtonDown"];
    if (func == null) return;
    
    func((event.pageX - event.srcElement.offsetLeft) / A_PixiApp.stage.scale.x, (event.pageY - event.srcElement.offsetTop) / A_PixiApp.stage.scale.y, 1);
}

private static JSystem_MouseUp(javascript.DomElementOnMouseEvent event)
{
    var func:Functor<(double, double)>;
    if (event.which == 1)
        func = cast<Map<string, Functor<(double, double)>>>(window)["__ALITTLEAPI_LButtonUp"];
    elseif (event.which == 2)
        func = cast<Map<string, Functor<(double, double)>>>(window)["__ALITTLEAPI_MButtonUp"];
    elseif (event.which == 3)
        func = cast<Map<string, Functor<(double, double)>>>(window)["__ALITTLEAPI_RButtonUp"];
    if (func == null) return;
    func((event.pageX - event.srcElement.offsetLeft) / A_PixiApp.stage.scale.x, (event.pageY - event.srcElement.offsetTop) / A_PixiApp.stage.scale.y);
}

private static JSystem_MouseWheel(javascript.DomElementOnMouseWheelEvent event)
{
    var func = cast<Map<string, Functor<(double, double)>>>(window)["__ALITTLEAPI_MouseWheel"];
    if (func == null) return;
    
    var x = event.wheelDelta;
    if (x == null) x = event.detail;
    var y = event.wheelDelta;
    if (y == null) y = event.detail;
    func(x, y);
}

private static JSystem_MouseOut(javascript.DomElementOnMouseEvent event)
{
    var func = cast<Map<string, Functor<()>>>(window)["__ALITTLEAPI_WindowLeave"];
    if (func == null) return;
    
	if (event.pageX - event.srcElement.offsetLeft > 0
        && event.pageY - event.srcElement.offsetTop > 0) return;
    
    func();
}

private static JSystem_KeyDown(javascript.DomElementOnKeyEvent event)
{
    var func = cast<Map<string, Functor<(int, int, int)>>>(window)["__ALITTLEAPI_KeyDown"];
    if (func == null) return;
    var mod = 0;
    if (event.altKey) mod = ALittle.UIEnumTypes.KMOD_ALT | mod;
    if (event.ctrlKey) mod = ALittle.UIEnumTypes.KMOD_CTRL | mod;
    if (event.shiftKey) mod = ALittle.UIEnumTypes.KMOD_SHIFT | mod;
    var key_code = KEY_CODE_MAP[event.keyCode];
    if (key_code == null) key_code = 0;
    var scan_code = SCAN_CODE_MAP[event.keyCode];
    if (scan_code == null) scan_code = 0;
    func(mod, key_code, scan_code);
}

private static JSystem_KeyUp(javascript.DomElementOnKeyEvent event)
{
    var func = cast<Map<string, Functor<(int, int, int)>>>(window)["__ALITTLEAPI_KeyUp"];
    if (func == null) return;
    var mod = 0;
    if (event.altKey) mod = ALittle.UIEnumTypes.KMOD_ALT | mod;
    if (event.ctrlKey) mod = ALittle.UIEnumTypes.KMOD_CTRL | mod;
    if (event.shiftKey) mod = ALittle.UIEnumTypes.KMOD_SHIFT | mod;
    var key_code = KEY_CODE_MAP[event.keyCode];
    if (key_code == null) key_code = 0;
    var scan_code = SCAN_CODE_MAP[event.keyCode];
    if (scan_code == null) scan_code = 0;
    func(mod, key_code, scan_code);
}

public static JSystem_CreateView(string title, int width, int height, int flag, double scale) : bool
{
    if (A_PixiApp != null) return true;
    
    var data = new PIXI.ApplicationData();
    if (window.wx != null)
    {
        var info = window.wx.getSystemInfoSync();
        __pixel_ratio = info.pixelRatio;
        data.view = window.canvas;
    }
    else
    {
        data.forceCanvas = !PIXI.utils.isWebGLSupported();
    }

    data.width = ALittle.Math_Floor(width * scale);
    data.height = ALittle.Math_Floor(height * scale);
    A_PixiApp = new PIXI.Application(data);
    document.body.appendChild(A_PixiApp.view);
    // 微信不能设置title
    if (window.wx == null) document.title = title;
    A_PixiApp.stage.scale.x = scale;
    A_PixiApp.stage.scale.y = scale;
    
	// 监听鼠标基本事件
    if (window.wx != null)
    {
        window.wx.onTouchStart(JSystem_FingerDown);
        window.wx.onTouchMove(JSystem_FingerMoved);
        window.wx.onTouchEnd(JSystem_FingerUp);
        window.wx.onTouchCancel(JSystem_FingerUp);
    }
    elseif (ALittle.System_IsPhone())
	{
		A_PixiApp.view.ontouchstart = JSystem_FingerDown;
		A_PixiApp.view.ontouchmove = JSystem_FingerMoved;
		A_PixiApp.view.ontouchend = JSystem_FingerUp;
		A_PixiApp.view.ontouchcancel = JSystem_FingerUp;
	}
	else
	{
		A_PixiApp.view.onmousedown = JSystem_MouseDown;
		A_PixiApp.view.onmousemove = JSystem_MouseMoved;
		A_PixiApp.view.onmouseup = JSystem_MouseUp;
		A_PixiApp.view.onmousewheel = JSystem_MouseWheel;
		A_PixiApp.view.onmouseout = JSystem_MouseOut;
		document.onkeydown = JSystem_KeyDown;
		document.onkeyup = JSystem_KeyUp;
	}
    
    A_JDisplaySystem.AddToStage(A_PixiApp.stage);
    
    var func = cast<Map<string, Functor<(int, int)>>>(window)["__ALITTLEAPI_ViewResized"];
    if (func != null) func(width, height);
    
    window.onresize = JSystem_HandleViewResized;
    A_PixiApp.ticker.add(JSystem_MainLoop);
    return true;
}

public static JSystem_HandleViewResized()
{
    var func = cast<Map<string, Functor<(int, int)>>>(window)["__ALITTLEAPI_ViewResized"];
    if (func != null) func(ALittle.System_GetScreenWidth(), ALittle.System_GetScreenHeight());
}

public static JSystem_SetViewTitle(string title) : bool
{
    document.title = title;
    return true;
}

public static JSystem_Render() : bool
{
    if (A_PixiApp == null) return false;
    A_PixiApp.render();
    return true;
}

public static JSystem_GetStage() : PIXI.Container
{
    if (A_PixiApp == null) return null;
    return A_PixiApp.stage;
}

private var LOOP_LAST_TIME = 0;
public static JSystem_MainLoop(double delta)
{
    if (LOOP_LAST_TIME == 0)
    {
        LOOP_LAST_TIME = javascript.Date.now();
        return;
    }
    
    var cur_time = javascript.Date.now();
    var delta_time = cur_time - LOOP_LAST_TIME;
    LOOP_LAST_TIME = cur_time;
    
    var func = cast<Map<string, Functor<(int)>>>(window)["__ALITTLEAPI_Update"];
    if (func != null) func(delta_time);
}