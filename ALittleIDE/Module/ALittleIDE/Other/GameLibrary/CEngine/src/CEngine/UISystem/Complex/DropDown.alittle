
namespace ALittle;

public class DropDown : TextCheckButton
{
    private DisplayLayout   _body;
    private DisplayObject   _background;
    private ScrollBar       _scrollbar;
    private Linear          _linear;
    private ScrollScreen    _scroll_screen;
    private List<string>    _data_list;
    
    private string          _button_style;
    private double          _body_margin;
    private double          _screen_margin_left;
    private double          _screen_margin_right;
    private double          _screen_margin_top;
    private double          _screen_margin_bottom;
    
    private int _show_count;

    public ctor(ControlSystem ctrl_sys)
    {
        this._body = new DisplayLayout(this._ctrl_sys);
        this._body.__right_data = this;
        this._body.visible = false;

        // this._background = null;
        // this._scrollbar = null;
    
        this._linear = new Linear(this._ctrl_sys);
        this._linear.type = UIEnumTypes.TYPE_V;
        this._linear.width_type = UIEnumTypes.SIZE_MARGIN;
        this._linear.width_value = 0;
        this._linear.height_type = UIEnumTypes.SIZE_MARGIN;
        this._linear.height_value = 0;
    
        this._scroll_screen = new ScrollScreen(this._ctrl_sys);
        this._scroll_screen.width_type = UIEnumTypes.SIZE_MARGIN;
        this._scroll_screen.width_value = 0;
        this._scroll_screen.height_type = UIEnumTypes.SIZE_MARGIN;
        this._scroll_screen.height_value = 0;
        this._scroll_screen.container = this._linear;
    
        this._body.AddChild(this._scroll_screen);

        // this._data_list = null;
        // this._button_style = null;
        this._body_margin = 0;
        this._screen_margin_left = 0;
        this._screen_margin_right = 0;
        this._screen_margin_top = 0;
        this._screen_margin_bottom = 0;
    
        this._show_count = 0;    // 显示个数
    
        this.AddEventListener(this, this.HandleButtonChange);
        this._body.AddEventListener(this, this.HandleBodyHide);
    }
    
    public set show_background(DisplayObject value)
    {
        if (this._background == value)
            return;
            
        this._body.RemoveChild(this._background);
        this._background = value;
        if (this._background != null)
        {
            this._background.width_type = UIEnumTypes.SIZE_MARGIN;
            this._background.height_type = UIEnumTypes.SIZE_MARGIN;
            this._background.width_value = 0;
            this._background.height_value = 0;
            this._body.AddChild(this._background, 0);
        }
    }
    public get show_background() : DisplayObject
    {
        return this._background;
    }
    
    public set show_scrollbar(ScrollBar value)
    {
        if (this._scrollbar == value)
            return;
            
        this._scrollbar = value;
        if (this._scrollbar == null)
            this._scroll_screen.right_scrollbar = null;
    
        this.Layout();
    }
    public get show_scrollbar() : ScrollBar
    {
        return this._scrollbar;
    }
    
    public set show_count(int value)
    {
        if (this._show_count == value)
            return;
        this._show_count = value;
    
        this.Layout();
    }
    public get show_count() : int
    {
        return this._show_count;
    }
    
    public set button_style(string value)
    {
        if (this._button_style == value)
            return;
        var child_list = this._scroll_screen.childs;
        for (var k, v in child_list)
            v.RemoveEventListener(this, this.HandleItemButtonClick);
            
        this._scroll_screen.RemoveAllChild();
        this._button_style = value;
        var button:TextButton = null;
        if (this._button_style != null && this._data_list != null)
        {
            for (var k, v in this._data_list)
            {
                button = this._ctrl_sys.CreateControl{TextButton}(this._button_style, null, this._scroll_screen);
                button.AddEventListener(this, this.HandleItemButtonClick);
                button.text = v;
                button.drag_trans_target = this._scroll_screen;
            }
        }
    
        if (this._show_count == 0 || this._show_count >= this._linear.child_count)
            this._body.height = this._linear.height + this._screen_margin_top + this._screen_margin_bottom;
        elseif (button != null)
            this._body.height = button.height * this._show_count + this._screen_margin_top + this._screen_margin_bottom;
        else
            this._body.height = 0;
    }
    public get button_style() : string
    {
        return this._button_style;
    }
    
    public set body_margin(double value)
    {
        this._body_margin = value;
    }
    public get body_margin() : double
    {
        return this._body_margin;
    }
    
    public set screen_margin_left(double value)
    {
        this._screen_margin_left = value;
    }
    public get screen_margin_left() : double
    {
        return this._screen_margin_left;
    }
    
    public set screen_margin_right(double value)
    {
        this._screen_margin_right = value;
    }
    public get screen_margin_right() : double
    {
        return this._screen_margin_right;
    }
    
    public set screen_margin_top(double value)
    {
        this._screen_margin_top = value;
    }
    public get screen_margin_top() : double
    {
        return this._screen_margin_top;
    }
    
    public set screen_margin_bottom(double value)
    {
        this._screen_margin_bottom = value;
    }
    public get screen_margin_bottom() : double
    {
        return this._screen_margin_bottom;
    }
    
    public set data_list(List<string> value)
    {
        if (value == null)
            value = new List<string>();

        var button_style = this._button_style;
        this._button_style = null;
        this._data_list = value;
        this.button_style = button_style;
    
        if (List_Len(value) > 0)
            this.text = value[1];
        else
            this.text = "";
        this.Layout();
    }
    public get data_list() : List<string>
    {
        return this._data_list;
    }

    protected fun HandleItemButtonClick(UIClickEvent event)
    {
        A_LayerManager.HideFromRight(this._body);
        if (this.text == event.target.text) return;
        this.text = event.target.text;
        this.DispatchEvent(new UISelectChangedEvent());
    }
    
    protected fun HandleButtonChange(UIChangedEvent event)
    {
        if (event.target.selected == false)
            A_LayerManager.HideFromRight(this._body);
        else
        {
            var abs_x, abs_y = this.LocalToGlobal();
            var adjust_y = abs_y + this.height * this.scale_y;
            if (A_UISystem.view_height < adjust_y + this._scroll_screen.height * this.scale_y)
                adjust_y = abs_y - this._scroll_screen.height * this.scale_y;

            this._body.y = adjust_y;
    
            this._body.width = this.width - this._body_margin;
            this._body.x = abs_x + this._body_margin / 2;
            this._body.scale_x = this.scale_x;
            this._body.scale_y = this.scale_y;
    
            this._scroll_screen.x = this._screen_margin_left;
            this._scroll_screen.y = this._screen_margin_top;
            this._scroll_screen.width_value = this._screen_margin_left + this._screen_margin_right;
            this._scroll_screen.height_value = this._screen_margin_top + this._screen_margin_bottom;
    
            A_LayerManager.ShowFromRight(this._body);
            A_UISystem.focus = this._scroll_screen;
        }
    }

    protected fun HandleBodyHide(UIHideEvent event)
    {
        if (this.selected == false)
            return;

        this.selected = false;
    }

    protected fun Layout()
    {
        if (this._show_count == 0 || this._scrollbar == null
                || this._show_count >= this._linear.child_count)
        {
            this._scroll_screen.right_scrollbar = null;
            this._body.height = this._linear.height + this._screen_margin_top + this._screen_margin_bottom;
        }
        else
        {
            this._scroll_screen.right_scrollbar = this._scrollbar;
            var button = this._linear.childs[1];
            if (button != null)
                this._body.height = button.height * this._show_count + this._screen_margin_top + this._screen_margin_bottom;
            else
                this._body.height = 0;
        }
    }
}