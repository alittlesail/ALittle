
namespace ALittle;

public using TextRadioButtonGroup = Map<TextRadioButton, bool>;

public class TextRadioButton : TextCheckButton
{
    private Map<TextRadioButton, bool> _group;

    public ctor(ControlSystem ctrl_sys)
    {
    }

    public set group(Map<TextRadioButton, bool> group)
    {
        if (this._group == group) return;
        if (this._group != null)
            this._group[this] = null;
        this._group = group;
        if (this._group != null)
            this._group[this] = true;
    }

    public get group() : Map<TextRadioButton, bool>
    {
        return this._group;
    }

    public static SetGroup(List<TextRadioButton> list)
    {
        var group:Map<TextRadioButton, bool> = CreateKeyWeakMap();
        for (var index, button in list)
        {
            if (button._group != null)
                button._group[button] = null;
            button._group = group;
            group[button] = true;
        }
    }

    protected fun HandleLButtonUp(UILButtonUpEvent event)
    {
        if (event.rel_x >= 0 && event.rel_y >= 0 && event.rel_x < event.target._width && event.rel_y < event.target._height)
        {
            if (this._selected == false)
            {
                this._selected = true;
                // 获取同组的单选按钮
                if (this._group != null)
                {
                    for (var k, _ in this._group)
                    {
                        // 设置为false
                        if (k != this && k._selected == true)
                        {
                            k._selected = false;
                            k.ShowUp();
                            k.DispatchEvent(new UIChangedEvent());
                        }
                    }
                }
                this.DispatchEvent(new UIChangedEvent());
            }
            var e = new UIClickEvent();
            e.is_drag = event.is_drag;
            e.count = event.count;
            this.DispatchEvent(e);
            if (System_IsPhone() == false)
                this.ShowOver();
            else
                this.ShowUp();
        }
        else
            this.ShowUp();
    }

    public set selected(bool value)
    {
        if (this._selected == value)
            return;
        this._selected = value;

        if (this._abs_disabled)
            this.ShowDisabled();
        else
            this.ShowUp();

        if (this._selected == false)
            return;

        // 获取同组的单选按钮
        if (this._group != null)
        {
            for (var k, _ in this._group)
            {
                // 设置为false
                if (k != this && k._selected == true)
                {
                    k._selected = false;
                    k.ShowUp();
                }
            }
        }
    }
}