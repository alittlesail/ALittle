
namespace ALittle;

public class SpringRadioButton : SpringCheckButton
{
    private Map<SpringRadioButton, bool> _group;
    private bool    _cancel_select;
    
    public ctor(ControlSystem ctrl_sys)
    {
        this._cancel_select = false;   // 是否允许取消选择
    }

    public set group(Map<SpringRadioButton, bool> group)
    {
        if (this._group == group) return;
        if (this._group != null)
            this._group[this] = null;
        this._group = group;
        if (this._group != null)
            this._group[this] = true;
    }

    public get group() : Map<SpringRadioButton, bool>
    {
        return this._group;
    }

    public static SetGroup(List<SpringRadioButton> list)
    {
        var group:Map<SpringRadioButton, bool> = CreateKeyWeakMap();
        for (var index, button in list)
        {
            if (button._group != null)
                button._group[button] = null;
            button._group = group;
            group[button] = true;
        }
    }
    
    public set cancel_select(bool value)
    {
        this._cancel_select = value;
    }
    
    public get cancel_select() : bool
    {
        return this._cancel_select;
    }

    protected fun HandleLButtonUp(UILButtonUpEvent event)
    {
        if (event.rel_x >= 0 && event.rel_y >= 0 && event.rel_x < event.target._width && event.rel_y < event.target._height)
        {
            if (this._selected == false)
            {
                this._selected = true;
                // 获取同组的单选按钮
                if (this._group != null)
                {
                    for (var k, _ in this._group)
                    {
                        // 设置为false
                        if (k != this && k._selected == true)
                        {
                            k._selected = false;
                            k.ShowUp();
                            k.DispatchEvent(new UIChangedEvent());
                        }
                    }
                }
                this.DispatchEvent(new UIChangedEvent());
            }
            elseif (this._cancel_select)
            {
                this._selected = false;
                this.DispatchEvent(new UIChangedEvent());
            }
            var e = new UIClickEvent();
            e.is_drag = event.is_drag;
            this.DispatchEvent(e);

            if (System_IsPhone() == false)
                this.ShowOver();
            else
                this.ShowUp();
        }
        else
        {
            this.ShowUp();
        }
    }
    
    public set selected(bool value)
    {
        if (this._selected == value)
            return;
        this._selected = value;
    
        if (this._abs_disabled)
            this.ShowDisabled();
        else
            this.ShowUp();
    
        if (this._selected == false)
            return;

        // 获取同组的单选按钮
        if (this._group != null)
        {
            for (var k, _ in this._group)
            {
                // 设置为false
                if (k != this && k._selected == true)
                {
                    k._selected = false;
                    k.ShowUp();
                }
            }
        }
    }
}