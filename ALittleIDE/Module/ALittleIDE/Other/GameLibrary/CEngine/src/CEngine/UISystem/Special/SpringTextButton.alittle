
namespace ALittle;

public class SpringTextButton : DisplayLayout
{
    private DisplayObject       _show_text;
    private DisplayObject       _show_disabled_text;
    
    private DisplayObject       _show_up;
    private DisplayObject       _show_disabled;
    
    private LoopGroup           _anti_loop;
    private bool                _big_or_small;
    private bool                _file_select;

    public ctor(ControlSystem ctrl_sys)
    {
        this._show_text = cast<DisplayObject>((new Map<string, any>()));
        this._show_text.width = 0;
        this._show_text.height = 0;
        this._show_text.text = "";
        this._show_text.visible = true;
    
        this._show_disabled_text = cast<DisplayObject>((new Map<string, any>()));
        this._show_disabled_text.width = 0;
        this._show_disabled_text.height = 0;
        this._show_disabled_text.text = "";
        this._show_disabled_text.visible = false;

        // this._show_up = null;
        // this._show_disabled = null;
        
        // this._anti_loop = null;
        this._big_or_small = false;
    
        // 兼容html，如果为true，那么按钮被点击的时候会调用文件选择框；如果为false，那么就是普通的按钮
        this._file_select = false;
    
        this.AddEventListener(this, this.HandleMoveIn);
        this.AddEventListener(this, this.HandleMoveOut);
        this.AddEventListener(this, this.HandleLButtonDown);
        this.AddEventListener(this, this.HandleLButtonUp);
        this.AddEventListener(this, this.HandleMButtonDown);
        this.AddEventListener(this, this.HandleMButtonUp);
        this.AddEventListener(this, this.ShowUp);
    
        this.AddEventListener(this, this.HandleFButtonDown);
        this.AddEventListener(this, this.HandleFButtonUp);
    
        this._pickup_rect = true;
        this._pickup_child = false;
    }
    
    public set disabled(bool value)
    {
        DisplayObject.disabled(this, value);
        if (value)
            this.ShowDisabled();
        else
            this.ShowUp(null);
    }
    
    ////////////////////////////////////////////////////////////////////////////////-
    protected fun HandleMoveIn(UIMoveInEvent event)
    {
        this.ShowOver();
    }

    protected fun HandleMoveOut(UIMoveOutEvent event)
    {
        this.ShowUp(null);
    }

    protected fun HandleLButtonDown(UILButtonDownEvent event)
    {
        this.ShowDown();
    }

    protected fun HandleLButtonUp(UILButtonUpEvent event)
    {
        if (event.rel_x >= 0 && event.rel_y >= 0 && event.rel_x < event.target._width && event.rel_y < event.target._height)
        {
            var e = new UIClickEvent();
            e.is_drag = event.is_drag;
            this.DispatchEvent(e);
            if (this._file_select)
                A_OtherSystem.SystemSelectFile(this);
            if (System_IsPhone == false)
                this.ShowOver();
            else
                this.ShowUp(null);
        }
        else
            this.ShowUp(null);
    }

    protected fun HandleMButtonDown(UIMButtonDownEvent event)
    {
        this.ShowDown();
    }

    protected fun HandleMButtonUp(UIMButtonUpEvent event)
    {
        if (event.rel_x >= 0 && event.rel_y >= 0 && event.rel_x < event.target._width && event.rel_y < event.target._height)
        {
            var e = new UIMClickEvent();
            e.is_drag = event.is_drag;
            this.DispatchEvent(e);
            if (System_IsPhone == false)
                this.ShowOver();
            else
                this.ShowUp(null);
        }
        else
            this.ShowUp(null);
    }

    protected fun HandleFButtonDown(UIFingerDownEvent event)
    {
        if (event.is_sfc == false)
            this.ShowDown();
    }

    protected fun HandleFButtonUp(UIFingerUpEvent event)
    {
        if (event.rel_x >= 0 && event.rel_y >= 0 && event.rel_x < event.target._width && event.rel_y < event.target._height)
        {
            var e = new UIFClickEvent();
            e.is_drag = event.is_drag;
            this.DispatchEvent(e);    
        }
        
        if (event.is_sfc == false)
            this.ShowUp(null);
    }
    
    ////////////////////////////////////////////////////////////////////////////////-
    public set text(any value)
    {
        this._show_text.text = value;
        this._show_disabled_text.text = value;
    }
    public get text() : string
    {
        return this._show_text.text;
    }
    
    ////////////////////////////////////////////////////////////////////////////////-
    protected fun ShowUp(UIFocusOutEvent event)
    {
        if (this._abs_disabled || this._disabled)
            return;
    
        if (this._show_up != null) this._show_up.alpha = 1;
        if (this._show_disabled != null) this._show_disabled.alpha = 0;
    
        this._show_text.visible = true;
        this._show_disabled_text.visible = false;
    
        // 把当前的缩放全部往原来的样子走
        this.ScaleTo(false, 200);
    }

    protected fun ShowDown()
    {
        if (this._abs_disabled || this._disabled)
            return;
    
        if (this._show_up != null) this._show_up.alpha = 1;
        if (this._show_disabled != null) this._show_disabled.alpha = 0;
    
        this._show_text.visible = true;
        this._show_disabled_text.visible = false;
    
        // 把当前的缩放全部往最大的样子走
        this.ScaleTo(true, 200);
    }

    protected fun ScaleTo(bool big_or_small, int time_in_ms)
    {
        if (this._big_or_small == big_or_small) return;
        this._big_or_small = big_or_small;
        var scale = 1.0;
        if (this._big_or_small) scale = 1.2;
    
        if (this._anti_loop != null) { this._anti_loop.Stop(); this._anti_loop = null; }
        this._anti_loop = new LoopGroup();
        this._anti_loop.AddUpdater(new LoopLinear(this, "scale_x", scale, time_in_ms, 1));
        this._anti_loop.AddUpdater(new LoopLinear(this, "scale_y", scale, time_in_ms, 1));
        this._anti_loop.Start();
    }

    protected fun ShowOver()
    {
        if (this._abs_disabled || this._disabled)
            return;
    
        if (this._show_up != null) this._show_up.alpha = 1;
        if (this._show_disabled != null) this._show_disabled.alpha = 0;
    
        this._show_text.visible = true;
        this._show_disabled_text.visible = false;
    
        // 把当前的缩放全部往原来的样子走
        this.ScaleTo(false, 200);
    }

    protected fun ShowDisabled()
    {
        this.ScaleTo(false, 200);
    
        if (this._show_up != null) this._show_up.alpha = 0;
        if (this._show_disabled != null) this._show_disabled.alpha = 1;
    
        this._show_text.visible = false;
        this._show_disabled_text.visible = true;
    }
    
    ////////////////////////////////////////////////////////////////////////////////-
    public set show_text(DisplayObject value)
    {
        if (value == null)
        {
            var show = this._show_text;
            this.RemoveChild(show);
            this._show_text = cast<DisplayObject>((new Map<string, any>()));
            this._show_text.width = 0;
            this._show_text.height = 0;
            this._show_text.text = show.text;
            this._show_text.visible = show.visible;
            return;
        }
        value.text = this._show_text.text;
        value.visible = this._show_text.visible;
        this.RemoveChild(this._show_text);
        this._show_text = value;
        this._show_text.x_type = UIEnumTypes.POS_ALIGN_CENTER;
        this._show_text.y_type = UIEnumTypes.POS_ALIGN_CENTER;
        this.AddChild(this._show_text, 6);
    }
    public get show_text() : DisplayObject
    {
        if (this._show_text._show == null)
            return null;
        return this._show_text;
    }
    
    public set show_disabled_text(DisplayObject value)
    {
        if (value == null)
        {
            var show = this._show_disabled_text;
            this.RemoveChild(show);
            this._show_disabled_text = cast<DisplayObject>((new Map<string, any>()));
            this._show_disabled_text.width = 0;
            this._show_disabled_text.height = 0;
            this._show_disabled_text.text = show.text;
            this._show_disabled_text.visible = show.visible;
            return;
        }
        value.text = this._show_disabled_text.text;
        value.visible = this._show_disabled_text.visible;
        this.RemoveChild(this._show_disabled_text);
        this._show_disabled_text = value;
        this._show_disabled_text.x_type = UIEnumTypes.POS_ALIGN_CENTER;
        this._show_disabled_text.y_type = UIEnumTypes.POS_ALIGN_CENTER;
        this.AddChild(this._show_disabled_text, 6);
    }
    public get show_disabled_text() : DisplayObject
    {
        if (this._show_disabled_text._show == null)
            return null;
        return this._show_disabled_text;
    }
    
    public set show_up(DisplayObject value)
    {
        this.RemoveChild(this._show_up);
        this._show_up = value;
        if (this._show_up != null)
        {
            this._show_up.width_type = UIEnumTypes.SIZE_MARGIN;
            this._show_up.height_type = UIEnumTypes.SIZE_MARGIN;
            this._show_up.width_value = 0;
            this._show_up.height_value = 0;
            this.AddChild(this._show_up, 1);
        }
        if (this._abs_disabled || this._disabled)
            this.ShowDisabled();
        else
            this.ShowUp(null);
    }
    public get show_up() : DisplayObject
    {
        return this._show_up;
    }
    
    public set show_disabled(DisplayObject value)
    {
        this.RemoveChild(this._show_disabled);
        this._show_disabled = value;
        if (this._show_disabled != null)
        {
            this._show_disabled.width_type = UIEnumTypes.SIZE_MARGIN;
            this._show_disabled.height_type = UIEnumTypes.SIZE_MARGIN;
            this._show_disabled.width_value = 0;
            this._show_disabled.height_value = 0;
            this.AddChild(this._show_disabled, 1);
        }
        if (this._abs_disabled || this._disabled)
            this.ShowDisabled();
        else
            this.ShowUp(null);
    }
    public get show_disabled() : DisplayObject
    {
        return this._show_disabled;
    }
    
    public set file_select(bool value)
    {
        this._file_select = value;
    }
    
    public get file_select():bool
    {
        return this._file_select;
    }
}