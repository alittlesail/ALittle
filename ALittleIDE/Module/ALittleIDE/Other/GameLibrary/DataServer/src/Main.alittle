
namespace DataServer;

protected var g_ConfigSystem:ALittle.IJsonConfig;
protected var g_ConnectServerYunIp = "";
protected var g_ConnectServerWanIp = "";
protected var g_ConnectServerPort = 0;

// 模块入口函数
public async static __Module_Setup(string sengine_path, string module_path, string config_path)
{
    alittle.Require(module_path, "Script/DataManager");
    alittle.Require(module_path, "Script/LoginManager");
    alittle.Require(module_path, "Script/LeaseManager");

    lua.math.randomseed(lua.os.time());
    g_ConfigSystem = ALittle.CreateJsonConfig(config_path, true);
    var wan_ip = g_ConfigSystem.GetConfig("wan_ip", "127.0.0.1");
    var yun_ip = g_ConfigSystem.GetConfig("yun_ip", "");
    var port_offset = g_ConfigSystem.GetConfig("port_offset", 0);
    var route_offset = g_ConfigSystem.GetConfig("route_offset", 0);
    
    __CPPAPI_ServerSchedule.StartMysqlQuery(1, g_ConfigSystem.GetString("main_conn_ip", null),
                                g_ConfigSystem.GetString("main_conn_username", null),
                                g_ConfigSystem.GetString("main_conn_password", null),
                                g_ConfigSystem.GetInt("main_conn_port", null),
                                g_ConfigSystem.GetString("main_conn_dbname", null));
    
    __CPPAPI_ServerSchedule.StartRouteSystem(ALittle.RouteType.RT_DATA, ALittle.RouteNum.RN_DEFAULT + route_offset);
    g_ConnectServerYunIp = yun_ip;
    g_ConnectServerWanIp = wan_ip;
    g_ConnectServerPort = ALittle.RoutePort.RT_DATA_BEGIN + port_offset + route_offset;
    __CPPAPI_ServerSchedule.CreateConnectServer(g_ConnectServerYunIp, g_ConnectServerWanIp, g_ConnectServerPort);
    __CPPAPI_ServerSchedule.CreateConnectClient(wan_ip, ALittle.RoutePort.RT_GATEWAY_CONNECT + port_offset);
    
    g_LeaseManager.Setup();
}

// 模块结束函数
public static __Module_Shutdown()
{
    g_LeaseManager.Shutdown();
}
