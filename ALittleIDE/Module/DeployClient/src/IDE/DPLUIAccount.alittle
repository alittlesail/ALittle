
namespace DeployClient;

protected var g_DPLWebLoginManager:AUIPlugin.AUIWebLoginManager;

protected class DPLUIAccount : ALittle.DisplayLayout
{
	// 账号登录相关
	private ALittle.Text _account_login;
	private ALittle.DropDown _account_dropdown;
	private ALittle.DisplayObject _account_icon;

	public fun TCtor()
	{
		// 初始化登录
		var client_ip = g_DPLServerConfig.GetConfig("client_ip", "139.159.176.119");
		var client_port = g_DPLServerConfig.GetConfig("client_port", 1800);
		
		g_DPLWebLoginManager = new AUIPlugin.AUIWebLoginManager();
		g_DPLWebLoginManager.AddEventListener(this, this.HandleAccountInReconnect);
		g_DPLWebLoginManager.AddEventListener(this, this.HandleAccountInLogout);
		g_DPLWebLoginManager.AddEventListener(this, this.HandleAccountInLogin);
		g_DPLWebLoginManager.Setup(client_ip, client_port, false, g_DPLConfig);
	}
	
	public fun Shutdown()
	{
		g_DPLWebLoginManager.Shutdown();
	}

	////////////////////////////////////////////////////////////////////////////////////////
	// 处理账号登录
	private fun HandleAccountLoginLB(ALittle.UILButtonDownEvent event)
	{
		g_DPLWebLoginManager.ShowLoginDialog();
	}
	// 已登录
	private fun HandleAccountInLogin(AUIPlugin.AUIWebAccountLoginEvent event)
	{
		this._account_login.visible = false;
		this._account_dropdown.visible = true;

		var data_list = new List<string>();
		data_list[1] = "修改密码";
		data_list[2] = "登出";
		this._account_dropdown.data_list = data_list;
		this._account_dropdown.text = g_DPLWebLoginManager.account_name;
	
		this._account_icon.x_value = this._account_dropdown.x_value + this._account_dropdown.width + 5;
	}
	// 已登出
	private fun HandleAccountInLogout(AUIPlugin.AUIWebAccountLogoutEvent event)
	{
		this._account_login.text = "请登录";
		this._account_login.underline = true;
		this._account_login.visible = true;
		this._account_login.disabled = false;
		this._account_dropdown.visible = false;
	
		this._account_icon.x_value = this._account_login.x_value + this._account_login.width + 5;
	}
	// 正在重连
	private fun HandleAccountInReconnect(AUIPlugin.AUIWebAccountReconnectEvent event)
	{
		this._account_login.text = "正在重连";
		this._account_login.underline = false;
		this._account_login.visible = true;
		this._account_login.disabled = true;
		this._account_dropdown.visible = false;
	
		this._account_icon.x_value = this._account_login.x_value + this._account_login.width + 5;
	}
	
	private fun HandleAccountDropdownChange(ALittle.UISelectChangedEvent event)
	{
		if (event.target.text == "登出")
			g_DPLWebLoginManager.Logout();
		elseif (event.target.text == "修改密码")
			g_DPLWebLoginManager.ShowPasswordDialog();
		
		this._account_dropdown.text = g_DPLWebLoginManager.account_name;
	}
}