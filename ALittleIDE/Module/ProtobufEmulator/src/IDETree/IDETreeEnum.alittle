
namespace Emulator;

public class IDETreeEnum : IDETreeLogic
{
	private ALittle.DropDown _value_dropdown;
	private lua.protobuf_message _msg;
	private lua.protobuf_reflection _rflct;
	private lua.protobuf_field_descriptor _field;

	private Map<string, int> _enum_value_map;

	public ctor(ALittle.ControlSystem ctrl_sys, RootInfo root, string field_name, lua.protobuf_reflection rflct, lua.protobuf_message msg, lua.protobuf_field_descriptor field)
	{
		this._rflct = rflct;
		this._msg = msg;
		this._field = field;

		var value = lua.protobuf.reflection_getenumvalue(this._rflct, this._msg, this._field);
		var enum_descriptor = lua.protobuf.fielddescriptor_enumtype(field);
		var value_count = lua.protobuf.enumdescriptor_valuecount(enum_descriptor);
		var data_list = new List<string>();
		this._enum_value_map = new Map<string, int>();
		var value_string = "";
		for (var i = 0; i < value_count; 1)
		{
			var enum_value = lua.protobuf.enumdescriptor_value(enum_descriptor, i);
            data_list[i + 1] = lua.protobuf.enumvaluedescriptor_name(enum_value);
			var enum_number = lua.protobuf.enumvaluedescriptor_number(enum_value);
			this._enum_value_map[data_list[i + 1]] = enum_number;
			if (enum_number == value) value_string = data_list[i + 1];
        }
		
		this._item = ctrl_sys.CreateControl{ALittle.DisplayObject}("ide_common_tree_enum", this);
		this.AddChild(this._item);
		this.width = this._item.width;
		this.height = this._item.height;

		this._item_title.text = "["..lua.protobuf.fielddescriptor_cpptypename(field).."] "..field_name..": ";
		this._value_dropdown.data_list = data_list;
		this._value_dropdown.text = value_string;
		this._value_dropdown.width = this.width - this._item_title.width - this._item_title.x - 2;
		this._value_dropdown.x = this._item_title.width + this._item_title.x;
		
		this.Init();
		this._value_dropdown.disabled = root.for_show;
	}

	public fun HandleSelectChanegd(ALittle.UISelectChangedEvent event)
	{
		var value = this._enum_value_map[event.target.text];
		if (value == null) return;

		lua.protobuf.reflection_setenumvalue(this._rflct, this._msg, this._field, value);
		this.Save();
    }
}