
namespace Emulator;

public class IDETreeEnumMapMessage : IDETree
{
	private IDETreeMap _parent;
	private DetailInfo _detail_info;
	
	private ALittle.DropDown _key_dropdown;
	private ALittle.TextButton _insert_button;
	private ALittle.TextButton _delete_button;

	private lua.protobuf_message _msg;
	private lua.protobuf_reflection _rflct;
	private lua.protobuf_field_descriptor _key_field;
	
	private Map<string, int> _enum_value_map;

	public ctor(ALittle.ControlSystem ctrl_sys, RootInfo root, IDETreeMap parent, lua.protobuf_reflection rflct, lua.protobuf_message msg, lua.protobuf_field_descriptor key_field, DetailInfo detail_info)
	{
		this._detail_info = detail_info;
		
		this._parent = parent;
		this._rflct = rflct;
		this._msg = msg;
		this._key_field = key_field;
				
		var value = lua.protobuf.reflection_getenumvalue(this._rflct, this._msg, this._key_field);
		var enum_descriptor = lua.protobuf.fielddescriptor_enumtype(this._key_field);
		var value_count = lua.protobuf.enumdescriptor_valuecount(enum_descriptor);
		var data_list = new List<string>();
		this._enum_value_map = new Map<string, int>();
		var value_string = "";
		for (var i = 0; i < value_count; 1)
		{
			var enum_value = lua.protobuf.enumdescriptor_value(enum_descriptor, i);
            data_list[i + 1] = lua.protobuf.enumvaluedescriptor_name(enum_value);
			var enum_number = lua.protobuf.enumvaluedescriptor_number(enum_value);
			this._enum_value_map[data_list[i + 1]] = enum_number;
			if (enum_number == value) value_string = data_list[i + 1];
        }

		// 对树的头部进行处理
		this._head = ctrl_sys.CreateControl{ALittle.DisplayObject}("ide_common_tree_enum_map_message", this);
		ALittle.DisplayGroup.AddChild(this, this._head, 1);
		
		this._item_title.text = "["..lua.protobuf.fielddescriptor_cpptypename(key_field).." "..detail_info.info.name.."] : ";
		this._key_dropdown.data_list = data_list;
		this._key_dropdown.text = value_string;
		
		this._key_dropdown.x = this._item_title.width + this._item_title.x + 1;
		this._insert_button.x = this._key_dropdown.x + this._key_dropdown.width + 1;
		this._delete_button.x = this._insert_button.x + this._insert_button.width + 1;
		this._head.width = this._delete_button.x + this._delete_button.width + 1;

		this.Init();
		this._key_dropdown.disabled = root.for_show;
		this._insert_button.disabled = root.for_show;
		this._delete_button.disabled = root.for_show;
	}

	public fun GetDetailInfo() : DetailInfo { return this._detail_info; }
	
	public fun HandleKeySelectChanegd(ALittle.UISelectChangedEvent event)
	{
		var value = this._enum_value_map[event.target.text];
		if (value == null) return;

		lua.protobuf.reflection_setenumvalue(this._rflct, this._msg, this._key_field, value);
		this.Save();
    }

	public fun HandleInsertClick(ALittle.UIClickEvent event)
	{
		this._parent.CreateOneBefore(this);
    }
	
	public fun HandleDeleteClick(ALittle.UIClickEvent event)
	{
		this._parent.Delete(this);
    }
}
