
namespace ALittle;

public class SpritePlay : Sprite
{
    private int _interval;
    private int _play_index;
    private LoopFunction _play_loop;

    public ctor(ControlSystem ctrl_sys)
    {
        this._interval = 0;        // 播放间隔
        this._play_index = 0;    // 当前播放的图片
        // this._play_loop = null;    // 循环函数
    }

    public set interval(int value)
    {
        if (this._interval == value)
            return;
        this._interval = value;
        this.Play();
    }
    
    public get interval() : int
    {
        return this._interval;
    }
    
    public fun Play()
    {
        if (this._play_loop != null)
        {
            A_WeakLoopSystem.RemoveUpdater(this._play_loop);
            this._play_loop = null;
        }

        // 播放位置为1
        this._play_index = 0;
        this._row_index = 1;
        this._col_index = 1;

        // 更新循环
        this._play_loop = new LoopFunction(bind(this.PlayUpdate, this), -1, this._interval, 0);
        A_WeakLoopSystem.AddUpdater(this._play_loop);
    }
    
    public fun Stop()
    {
        if (this._play_loop != null)
        {
            A_WeakLoopSystem.RemoveUpdater(this._play_loop);
            this._play_loop = null;
        }
    }

    protected fun PlayUpdate()
    {
        var total_count = this._row_count * this._col_count;
        // 隐藏当前的图片
        ++ this._play_index;
        this._play_index = this._play_index % total_count;
        this.row_index = lua.math.floor(this._play_index / this._row_count) + 1;
        this.col_index = this._play_index % this._col_count + 1;
    }
}