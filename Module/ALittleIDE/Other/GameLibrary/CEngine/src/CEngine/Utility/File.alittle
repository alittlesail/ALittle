
namespace ALittle;

// 获取程序的基本文件路径
public static File_BaseFilePath() : string
{
    [Language(Lua)]
    return __CPPAPI_ScriptSystem.BaseFilePath();
    [Language(JavaScript)]
    return "";
}

// 获取程序的扩展文件路径
public static File_ExternalFilePath() : string
{
    [Language(Lua)]
    return __CPPAPI_ScriptSystem.ExternalFilePath();
    [Language(JavaScript)]
    return "";
}

// 复制文件
// src_path 要复制的源文件路径
// dst_path 目标文件路径
public static File_CopyFile(string src_path, string dst_path) : bool
{
    [Language(Lua)]
    return lua.__CPPAPI_CopyFile(src_path, dst_path, false);
    [Language(JavaScript)]
    Error("not support File_CopyFile");
    [Language(JavaScript)]
    return false;
}
public static File_CopyFileFromAsset(string src_path, string dst_path) : bool
{
    [Language(Lua)]
    return lua.__CPPAPI_CopyFile(src_path, dst_path, true);
    [Language(JavaScript)]
    Error("not support File_CopyFileFromAsset");
    [Language(JavaScript)]
    return false;
}

// 把字符串保存到文件里面去
// target_path 保存的文件路径
// content 字符串内容
// size 要保存的字节数，如果填0表示content全部保存
public static File_SaveFile(string target_path, string content, int size) : bool
{
    [Language(Lua)]
    return lua.__CPPAPI_SaveFile(target_path, content, size);
    [Language(JavaScript)]
    Error("not support File_SaveFile");
    [Language(JavaScript)]
    return false;
}

// 深度复制文件夹
// src_path 原文件夹
// dest_path 目标文件夹
// ext 目标后缀名的文件，如果是null表示所有文件
// log 是否打印日志
public static File_CopyDeepDir(string src_path, string dest_path, [Nullable] string ext, [Nullable] bool log)
{
    [Language(Lua)]
    {
        var upper_ext:string = null;
        if (ext != null) upper_ext = lua.String.upper(ext);
    
        // 遍历源文件夹
        for (var file in lua.lfs.dir(src_path))
        {
            // 跳过当前目录，父级目录
            if (file != "." && file != "..")
            {
                // 源文件路径
                var src_file_path = src_path .. "/" .. file;
                // 目标文件路径
                var dest_file_path = dest_path .. "/" .. file;
                // 获取源文件属性
                var attr = lua.lfs.attributes(src_file_path);
                // 如果是目录进行递归复制
                if (attr.mode == "directory")
                {
                    // 创建文件夹
                    lua.lfs.mkdir(dest_file_path);
                    // 开始复制
                    File_CopyDeepDir(src_file_path, dest_file_path, upper_ext, log);
                }
                // 如果是文件，就直接复制文件
                else
                {
                    if (upper_ext == null || File_GetFileExtByPathAndUpper(src_file_path) == upper_ext)
                    {
                        File_CopyFile(src_file_path, dest_file_path);
                        if (log) Log("copy file:", src_file_path, dest_file_path);
                    }
                }
            }
        }
    }
}

// 直接从文件读入文本，只从现有的目录里面找
// file_path 表示文件路径
// crypt_mode 表示是否执行解密
public static File_ReadTextFromFile(string file_path, [Nullable] bool crypt_mode) : string
{
    // 读取文件
    [Language(Lua)]
    {
        var file = new lua.__CPPAPILocalFile();
        file.SetPath(file_path);
        if (file.Load() == false) return null;
        if (crypt_mode) file.Decrypt(null);
        var content = file.GetContent();
        file.Clear();
        return content;   
    }
    
    [Language(JavaScript)]
    Error("not support File_ReadTextFromFile");
    [Language(JavaScript)]
    return null;
}

// 直接把文本写入现有目录
// content 内容
// file_path 文件路径
public static File_WriteTextToFile(string content, string file_path) : bool
{
    return File_SaveFile(file_path, content, -1);
}

// 直接从文件读入Json，先从现有的目录里面找，没有的话再从Asset里面找
// file_path 表示文件路径
// crypt_mode 表示是否执行解密
public static File_ReadJsonFromFile(string file_path, [Nullable] bool crypt_mode) : any, string
{
    // 读取文件
    [Language(Lua)]
    {
        var file = new lua.__CPPAPILocalFile();
        file.SetPath(file_path);
        if (file.Load() == false) return null, file_path.." load failed";
        if (crypt_mode) file.Decrypt(null);
    
        var content = file.GetContent();
        file.Clear();
        var error, new_content = tcall(lua.json.decode, content);
        if (error == null) return new_content, content;
        return null, new_content;
    }
    
    [Language(JavaScript)]
    Error("not support File_ReadJsonFromFile");
    [Language(JavaScript)]
    return null, "not support File_ReadJsonFromFile";
}

// 直接从Asset文件读入Json
// file_path 表示文件路径
// crypt_mode 表示是否执行解密
public static File_ReadJsonFromAsset(string file_path, bool crypt_mode) : any, string
{
    // 读取文件
    [Language(Lua)]
    {
        var file = new lua.__CPPAPILocalFile();
        file.SetPath(file_path);
        if (file.LoadBySDL() == false) return null, file_path.." load failed!";
        if (crypt_mode) file.Decrypt(null);
    
        var content = file.GetContent();
        file.Clear();
        var error, new_content = tcall(lua.json.decode, content);
        if (error == null) return new_content, content;
        return null, new_content;
    }
    
    [Language(JavaScript)]
    Error("not support File_ReadJsonFromAsset");
    [Language(JavaScript)]
    return null, "not support File_ReadJsonFromAsset";
}

// 直接把Json文件写入现有目录
// content table对象
// file_path 文件路径
public static File_WriteJsonToFile(any content, string file_path) : bool
{
    return File_SaveFile(file_path, lua.json.encode(content), -1);
}

// 删除指定时间之前的日志文件
// day_count_before 表示删除几天前的日志
public static DeleteLog(int day_count_before)
{
    [Language(Lua)]
    {
        if (day_count_before <= 0)
            return;
    
        var log_path = File_ExternalFilePath() .. "Log";
        if (File_GetFileAttr(log_path) == null)
            return;
    
        var time_table = new lua.dateinfo();
        time_table.day = time_table.day - day_count_before;
        var time_string = lua.os.date("%Y-%m-%d", lua.os.time(time_table))..".log";
    
        var delete_list = new List<string>();
        var delete_count = 0;
        for (var file in lua.lfs.dir(log_path))
        {
            if (file != "." && file != "..")
            {
                if (file <= time_string)
                {
                    ++ delete_count;
                    delete_list[delete_count] = file;
                }
            }
        }
        
        for (var k, v in delete_list)
            File_DeleteFile(File_ExternalFilePath() .. "Log/" .. v);
    }
    
    [Language(JavaScript)]
    Error("not support DeleteLog");
}